---
description: ReSpeaker XVF3800 USB 4-Mic Array æ˜¯ä¸€æ¬¾ä¸“ä¸šçš„åœ†å½¢éº¦å…‹é£é˜µåˆ—ï¼Œå…·æœ‰ AECã€æ³¢æŸæˆå½¢ã€å™ªå£°æŠ‘åˆ¶å’Œ 360Â° è¯­éŸ³æ•è·åŠŸèƒ½ã€‚ä¸ XIAO ESP32S3 é…å¯¹ä½¿ç”¨ï¼Œå¯ä¸ºæ™ºèƒ½è®¾å¤‡ã€æœºå™¨äººå’Œç‰©è”ç½‘åº”ç”¨æä¾›å…ˆè¿›çš„è¯­éŸ³æ§åˆ¶ã€‚æ¢ç´¢æ— ç¼é›†æˆå’ŒåŒæ¨¡å¼çµæ´»æ€§ã€‚

title: reSpeaker XVF3800 é€šè¿‡ I2C è¿›è¡ŒéŸ³é¢‘æ’­æ”¾å’ŒéŸ³é‡æ§åˆ¶

keywords:
- reSpeaker
- XIAO
- ESP32S3
image: https://files.seeedstudio.com/wiki/respeaker_xvf3800_usb/6-ReSpeaker-XVF3800-4-Mic-Array-With-XIAO-ESP32S3.jpg
slug: /cn/respeaker_xvf3800_xiao_volume
last_update:
  date: 7/16/2025
  author: Kasun Thushara
---

æ­¤ä»£ç ä½¿ **XIAO ESP32S3** é€šè¿‡ I2S æ¥å£ä½¿ç”¨ **å¤–éƒ¨ AIC3104 éŸ³é¢‘ç¼–è§£ç å™¨èŠ¯ç‰‡** æ’­æ”¾ **440Hz** éŸ³è°ƒã€‚å®ƒå°†éŸ³é¢‘è¾“å‡ºå‘é€åˆ° **è€³æœº (HPLOUT) æˆ–çº¿è·¯è¾“å‡º (LOP)**ï¼Œå¹¶å…è®¸æ‚¨é€šè¿‡åœ¨ä¸²è¡Œç›‘è§†å™¨ä¸­è¾“å…¥ `+` æˆ– `-` æ¥è°ƒèŠ‚éŸ³é‡ã€‚éŸ³è°ƒæ˜¯åœ¨ä»£ç ä¸­ç”Ÿæˆçš„ï¼ˆä¸æ˜¯ä»æ–‡ä»¶ä¸­ï¼‰ï¼Œä½¿ç”¨æ­£å¼¦æ³¢ã€‚å®ƒè¿˜å…è®¸æ‚¨é€šè¿‡è¾“å…¥ m æ¥åˆ‡æ¢è¾“å‡ºæ¨¡å¼ï¼ˆè€³æœºæˆ–çº¿è·¯è¾“å‡ºï¼‰ã€‚AIC3104 é€šè¿‡ **I2C** æ§åˆ¶ï¼Œç”¨äºè®¾ç½®éŸ³é‡å’Œè¾“å‡ºåŠŸç‡çš„å¯„å­˜å™¨ã€‚

<p style={{textAlign: 'center'}}><img src="https://files.seeedstudio.com/wiki/respeaker_xvf3800_usb/front-xiao.jpg" alt="pir" width={600} height="auto" /></p>

<div class="get_one_now_container" style={{textAlign: 'center'}}>
    <a class="get_one_now_item" href="https://www.seeedstudio.com/ReSpeaker-XVF3800-4-Mic-Array-With-XIAO-ESP32S3-p-6489.html" target="_blank">
            <strong><span><font color={'FFFFFF'} size={"4"}> ç«‹å³è·å– ğŸ–±ï¸</font></span></strong>
    </a>
</div>

## å·¥ä½œåŸç†

ä»£ç é¦–å…ˆé€šè¿‡ I2C æ¥å£åˆå§‹åŒ– **AIC3104 éŸ³é¢‘ç¼–è§£ç å™¨**ï¼Œé…ç½®å…³é”®è¾“å‡ºå‚æ•°ï¼Œå¦‚ DAC éŸ³é‡å’Œè€³æœº/çº¿è·¯è¾“å‡ºç”µå¹³ã€‚æ¥ä¸‹æ¥ï¼Œå®ƒä½¿ç”¨ AudioTools åº“çš„ SineWaveGenerator ç»„ä»¶ç”Ÿæˆ **æ­£å¼¦æ³¢ä¿¡å·**ï¼ˆç‰¹åˆ«æ˜¯ 440 Hz éŸ³è°ƒï¼‰ã€‚ç„¶åé€šè¿‡ **I2S æ¥å£** æµå¼ä¼ è¾“æ­¤éŸ³é¢‘ä¿¡å·ï¼Œè¯¥æ¥å£é…ç½®ä¸ºå°†æ•°å­—éŸ³é¢‘æ•°æ®ä» ESP32S3 å‘é€åˆ° AIC3104 èŠ¯ç‰‡ã€‚ä½¿ç”¨ StreamCopy å¯¹è±¡å°†æ­£å¼¦æ³¢è¿ç»­é¦ˆé€åˆ°ç¼–è§£ç å™¨ï¼Œè¯¥å¯¹è±¡å¤„ç†éŸ³é¢‘æ•°æ®åˆ° I2S æµçš„å®æ—¶ä¼ è¾“ã€‚åŒæ—¶ï¼Œç”¨æˆ·å¯ä»¥é€šè¿‡ **ä¸²è¡Œç›‘è§†å™¨** ä¸ç³»ç»Ÿäº¤äº’ï¼Œå…è®¸å®æ—¶æ§åˆ¶ **éŸ³é‡çº§åˆ«** å’Œ **è¾“å‡ºæ¨¡å¼ï¼ˆè€³æœºæˆ–çº¿è·¯è¾“å‡ºï¼‰**ã€‚

## ä¸²è¡Œå‘½ä»¤

ç³»ç»Ÿå“åº”è¾“å…¥åˆ°ä¸²è¡Œç›‘è§†å™¨ä¸­çš„ç®€å•ä¸²è¡Œå‘½ä»¤ã€‚æŒ‰ `+` é”®å°†éŸ³é‡å¢åŠ ä¸€çº§ï¼Œè€ŒæŒ‰ `-` é”®å°†é™ä½éŸ³é‡ã€‚å¦‚æœæŒ‰ `m` é”®ï¼Œè¾“å‡ºæ¨¡å¼å°† **åœ¨è€³æœºè¾“å‡º (HPLOUT) å’Œçº¿è·¯è¾“å‡º (LOP) ä¹‹é—´åˆ‡æ¢**ã€‚è¿™äº›å‘½ä»¤å…è®¸å®æ—¶æµ‹è¯•å’Œè°ƒæ•´éŸ³é¢‘è¾“å‡ºï¼Œæ— éœ€ä¿®æ”¹æˆ–é‡æ–°ä¸Šä¼ ä»£ç ã€‚

## ä»£ç 

```bash
#include <Wire.h>
#include "AudioTools.h"

// AIC3104 I2C address
#define AIC3104_ADDR 0x18

// Register addresses
#define AIC3104_PAGE_CTRL        0x00
#define AIC3104_LEFT_DAC_VOLUME  0x2B
#define AIC3104_RIGHT_DAC_VOLUME 0x2C
#define AIC3104_HPLOUT_LEVEL     0x33
#define AIC3104_HPROUT_LEVEL     0x41
#define AIC3104_LEFT_LOP_LEVEL   0x56
#define AIC3104_RIGHT_LOP_LEVEL  0x5D

// Audio: 16kHz, stereo, 16-bit
AudioInfo info(16000, 2, 16);
SineWaveGenerator<int16_t> sineWave(32000);
GeneratedSoundStream<int16_t> sound(sineWave);
I2SStream out;
StreamCopy copier(out, sound);

// Volume: range 0â€“17 (0â€“8 = DAC, 9â€“17 = analog boost)
int volume = 8;
bool useHPOUT = true; // true = use HPLOUT, false = use LOP

void aic3104_reg_write(uint8_t reg, uint8_t val) {
  Wire.beginTransmission(AIC3104_ADDR);
  Wire.write(reg);
  Wire.write(val);
  Wire.endTransmission();
}

void setupAIC3104() {
  Wire.begin();
  aic3104_reg_write(AIC3104_PAGE_CTRL, 0x00);

  // Set default 0dB DAC volume
  aic3104_reg_write(AIC3104_LEFT_DAC_VOLUME, 0x00);
  aic3104_reg_write(AIC3104_RIGHT_DAC_VOLUME, 0x00);

  // Set output to 0dB, unmuted, powered up
  aic3104_reg_write(AIC3104_HPLOUT_LEVEL, 0x0D);
  aic3104_reg_write(AIC3104_HPROUT_LEVEL, 0x0D);
  aic3104_reg_write(AIC3104_LEFT_LOP_LEVEL, 0x0B);
  aic3104_reg_write(AIC3104_RIGHT_LOP_LEVEL, 0x0B);
}

void setVolume(int vol) {
  vol = constrain(vol, 0, 17);
  volume = vol;

  if (vol <= 8) {
    // DAC attenuation
    uint8_t dacVal = vol * 9; // 0dB to -72dB
    aic3104_reg_write(AIC3104_LEFT_DAC_VOLUME, dacVal);
    aic3104_reg_write(AIC3104_RIGHT_DAC_VOLUME, dacVal);

    // Output level fixed to 0dB
    aic3104_reg_write(AIC3104_HPLOUT_LEVEL, 0x0D);
    aic3104_reg_write(AIC3104_HPROUT_LEVEL, 0x0D);
    aic3104_reg_write(AIC3104_LEFT_LOP_LEVEL, 0x0B);
    aic3104_reg_write(AIC3104_RIGHT_LOP_LEVEL, 0x0B);
  } else {
    // DAC at 0dB
    aic3104_reg_write(AIC3104_LEFT_DAC_VOLUME, 0x00);
    aic3104_reg_write(AIC3104_RIGHT_DAC_VOLUME, 0x00);

    // Boost output gain via HPLOUT or LOP
    uint8_t gain = (vol - 8); // from +1 to +9 dB
    uint8_t outVal = (gain << 4) | 0x0B; // Set gain and power/mute bits

    if (useHPOUT) {
      aic3104_reg_write(AIC3104_HPLOUT_LEVEL, outVal);
      aic3104_reg_write(AIC3104_HPROUT_LEVEL, outVal);
    } else {
      aic3104_reg_write(AIC3104_LEFT_LOP_LEVEL, outVal);
      aic3104_reg_write(AIC3104_RIGHT_LOP_LEVEL, outVal);
    }
  }

  // Debug info
  Serial.print("Volume set to ");
  Serial.print(volume);
  Serial.print(" (");
  if (vol <= 8) Serial.print("-" + String(volume * 1) + " dB)");
  else Serial.print("+" + String((vol - 8)) + " dB)");
  Serial.println();
}

void setup() {
  Serial.begin(115200);
  while (!Serial);

  setupAIC3104();
  setVolume(volume);

  auto config = out.defaultConfig(TX_MODE);
  config.copyFrom(info);
  config.pin_bck = 8;
  config.pin_ws = 7;
  config.pin_data = 44;
  config.is_master = true;

  out.begin(config);
  sineWave.begin(info, N_A4); // 440Hz tone
}

void loop() {
  copier.copy();

  if (Serial.available()) {
    char c = Serial.read();

    if (c == '+' && volume < 17) {
      setVolume(volume + 1);
    } else if (c == '-' && volume > 0) {
      setVolume(volume - 1);
    } else if (c == 'm') {
      useHPOUT = !useHPOUT;
      setVolume(volume);
      Serial.print("Switched to ");
      Serial.println(useHPOUT ? "HPLOUT (headphone)" : "LOP (line out)");
    }
  }
}

```

<p style={{textAlign: 'center'}}><img src="https://files.seeedstudio.com/wiki/respeaker_xvf3800_usb/volumecontrol.PNG" alt="pir" width={600} height="auto" /></p>

## æŠ€æœ¯æ”¯æŒä¸äº§å“è®¨è®º

æ„Ÿè°¢æ‚¨é€‰æ‹©æˆ‘ä»¬çš„äº§å“ï¼æˆ‘ä»¬åœ¨è¿™é‡Œä¸ºæ‚¨æä¾›ä¸åŒçš„æ”¯æŒï¼Œä»¥ç¡®ä¿æ‚¨ä½¿ç”¨æˆ‘ä»¬äº§å“çš„ä½“éªŒå°½å¯èƒ½é¡ºç•…ã€‚æˆ‘ä»¬æä¾›å¤šç§æ²Ÿé€šæ¸ é“ï¼Œä»¥æ»¡è¶³ä¸åŒçš„åå¥½å’Œéœ€æ±‚ã€‚

<div class="button_tech_support_container">
<a href="https://forum.seeedstudio.com/" class="button_forum"></a> 
<a href="https://www.seeedstudio.com/contacts" class="button_email"></a>
</div>

<div class="button_tech_support_container">
<a href="https://discord.gg/eWkprNDMU7" class="button_discord"></a> 
<a href="https://github.com/Seeed-Studio/wiki-documents/discussions/69" class="button_discussion"></a>
</div>