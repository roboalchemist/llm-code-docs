---
description: XIAO ESP32 Sense ä½¿ç”¨ä¸åŒç¡çœ æ¨¡å¼çš„åŠŸè€—
title: XIAO ESP32S3 Sense ç¡çœ æ¨¡å¼
keywords:
- Sleep_Modes
image: https://files.seeedstudio.com//wiki/ESP32S3_Sense_SleepMode/1.png
slug: /cn/XIAO_ESP32S3_Consumption
last_update:
  date: 08/27/2024
  author: Jason
---


import Tabs from '@theme/Tabs';
import TabItem from '@theme/TabItem';

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/ESP32S3_Sense_SleepMode/background02.png" style={{width:800, height:'auto'}}/></div>

åœ¨è¿™é‡Œï¼Œæˆ‘å°†å±•ç¤ºä¸€äº›ç®€å•çš„ç¤ºä¾‹æ¥æ¼”ç¤ºè¿™äº›ä½åŠŸè€—ç¡çœ æ¨¡å¼çš„ä½¿ç”¨ã€‚æ‰€æœ‰ ESP32 å¼€å‘æ¿éƒ½æ˜¯å¤šåŠŸèƒ½çš„ï¼Œæˆ‘åœ¨æ­¤ä¸Šä¸‹æ–‡ä¸­ä½¿ç”¨çš„å¼€å‘æ¿æ˜¯ XIAO ESP32S3 Senseã€‚

## ç¡¬ä»¶æ¦‚è§ˆ

<div class="table-center">
  <table align="center">
    <tr>
        <th>Seeed Studio XIAO ESP32S3 Sense</th>
    </tr>
    <tr>
        <td><div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/SeeedStudio-XIAO-ESP32S3/img/xiaoesp32s3sense.jpg" style={{width:300, height:'auto'}}/></div></td>
    </tr>
      <tr>
        <td><div class="get_one_now_container" style={{textAlign: 'center'}}>
          <a class="get_one_now_item" href="https://www.seeedstudio.com/XIAO-ESP32S3-Sense-p-5639.html" target="_blank">
              <strong><span><font color={'FFFFFF'} size={"4"}> ç«‹å³è·å– ğŸ–±ï¸</font></span></strong>
          </a>
      </div></td>
    </tr>
  </table>
</div>

## æ·±åº¦ç¡çœ 

### ä»‹ç»

åœ¨æ·±åº¦ç¡çœ æ¨¡å¼ä¸‹ï¼ŒESP32 ä¼šå…³é—­ CPUã€å¤§éƒ¨åˆ† RAM ä»¥åŠæ‰€æœ‰ç”± APB_CLK æ—¶é’Ÿé©±åŠ¨çš„æ•°å­—å¤–è®¾ã€‚åªæœ‰ä»¥ä¸‹ç»„ä»¶ä¿æŒä¾›ç”µï¼š

- RTC æ§åˆ¶å™¨
- ULP åå¤„ç†å™¨
- RTC å¿«é€Ÿå­˜å‚¨å™¨
- RTC æ…¢é€Ÿå­˜å‚¨å™¨

### å”¤é†’æ–¹æ³•

- **å®šæ—¶å™¨å”¤é†’ï¼š**ESP32 å¯ä»¥é€šè¿‡è®¾ç½®å®šæ—¶å™¨åœ¨æŒ‡å®šæ—¶é—´åè‡ªåŠ¨å”¤é†’ã€‚

- **è§¦æ‘¸æ¿ä¸­æ–­å”¤é†’ï¼š**è®¾å¤‡å¯ä»¥é€šè¿‡è§¦æ‘¸æ¿æ´»åŠ¨å”¤é†’ï¼Œé€‚ç”¨äºéœ€è¦ç”¨æˆ·äº¤äº’çš„åº”ç”¨ã€‚

- **å¤–éƒ¨å”¤é†’ï¼š**ESP32 å¯ä»¥é€šè¿‡å¤–éƒ¨ä¿¡å·ï¼ˆå¦‚æŒ‰é’®æŒ‰å‹ï¼‰å”¤é†’ï¼Œéå¸¸é€‚åˆä½åŠŸè€—åº”ç”¨ã€‚

- **ULP åå¤„ç†å™¨æ´»åŠ¨å”¤é†’ï¼š**ULP åå¤„ç†å™¨å¯ä»¥ç‹¬ç«‹è¿è¡Œï¼Œç›‘æ§ç‰¹å®šæ¡ä»¶å¹¶å”¤é†’ä¸» CPU ä»¥èŠ‚çœåŠŸè€—ã€‚

- **GPIO å”¤é†’ï¼š**è®¾å¤‡å¯ä»¥é€šè¿‡ GPIO å¼•è„šçŠ¶æ€å˜åŒ–ï¼ˆé«˜ç”µå¹³æˆ–ä½ç”µå¹³ï¼‰å”¤é†’ï¼Œä¸ºå„ç§ä¼ æ„Ÿå™¨å’Œå¤–è®¾æä¾›çµæ´»æ€§ã€‚

ä¸‹é¢ç»™å‡ºä¸‰ä¸ª XIAO ESP32 S3 Sense ä½¿ç”¨æ·±åº¦ç¡çœ æ¨¡å¼çš„ç®€å•ç¤ºä¾‹ã€‚

### ä»£ç å®ç°

<Tabs>
<TabItem  value="DeepSleepExample1" label="TimerWakeUP" default>

```cpp

#define uS_TO_S_FACTOR 1000000ULL 
#define TIME_TO_SLEEP  5       

RTC_DATA_ATTR int bootCount = 0;

void print_wakeup_reason() {
  esp_sleep_wakeup_cause_t wakeup_reason;

  wakeup_reason = esp_sleep_get_wakeup_cause();

  switch (wakeup_reason) {
    case ESP_SLEEP_WAKEUP_EXT0:     Serial.println("Wakeup caused by external signal using RTC_IO"); break;
    case ESP_SLEEP_WAKEUP_EXT1:     Serial.println("Wakeup caused by external signal using RTC_CNTL"); break;
    case ESP_SLEEP_WAKEUP_TIMER:    Serial.println("Wakeup caused by timer"); break;
    case ESP_SLEEP_WAKEUP_TOUCHPAD: Serial.println("Wakeup caused by touchpad"); break;
    case ESP_SLEEP_WAKEUP_ULP:      Serial.println("Wakeup caused by ULP program"); break;
    default:                        Serial.printf("Wakeup was not caused by deep sleep: %d\n", wakeup_reason); break;
  }
}

void setup() {
  Serial.begin(115200);
  delay(1000);  


  ++bootCount;
  Serial.println("Boot number: " + String(bootCount));


  print_wakeup_reason();


  esp_sleep_enable_timer_wakeup(TIME_TO_SLEEP * uS_TO_S_FACTOR);
  Serial.println("Setup ESP32 to sleep for every " + String(TIME_TO_SLEEP) + " Seconds");

  Serial.println("Going to sleep now");
  Serial.flush();
  esp_deep_sleep_start();
  Serial.println("This will never be printed");
}

void loop() {

}
```

### Detailed Notes

```cpp
#define uS_TO_S_FACTOR 1000000ULL 
```

- å®šä¹‰ä¸€ä¸ªå®æ¥å°†å¾®ç§’è½¬æ¢ä¸ºç§’ã€‚1000000ULLæ˜¯ç”¨äºå°†å¾®ç§’è½¬æ¢ä¸ºç§’çš„å› å­ã€‚

```cpp
#define TIME_TO_SLEEP  5     
```

- å®šä¹‰ä¸€ä¸ªå®æ¥è®¾ç½® ESP32 å°†è¿›å…¥ç¡çœ çŠ¶æ€çš„æ—¶é—´ï¼Œåœ¨è¿™ç§æƒ…å†µä¸‹ä¸º 5 ç§’ã€‚

```cpp
RTC_DATA_ATTR int bootCount = 0;
```

- å£°æ˜ä¸€ä¸ªæ•´å‹å˜é‡ `bootCount`ï¼Œå¸¦æœ‰ `RTC_DATA_ATTR` å±æ€§ï¼Œè¿™å…è®¸å®ƒåœ¨æ·±åº¦ç¡çœ æœŸé—´ä¿æŒå…¶å€¼ã€‚

```cpp
void print_wakeup_reason() {
```

- å®šä¹‰ä¸€ä¸ªåä¸º `print_wakeup_reason()` çš„å‡½æ•°ï¼Œç”¨äºæ‰“å° ESP32 å”¤é†’çš„åŸå› ã€‚

```cpp
esp_sleep_wakeup_cause_t wakeup_reason;
```

- å£°æ˜ä¸€ä¸ªç±»å‹ä¸º `esp_sleep_wakeup_cause_t` çš„å˜é‡ `wakeup_reason` æ¥å­˜å‚¨å”¤é†’äº‹ä»¶çš„åŸå› ã€‚

```cpp
wakeup_reason = esp_sleep_get_wakeup_cause();
```

- è°ƒç”¨å‡½æ•° `esp_sleep_get_wakeup_cause()` æ¥è·å–å”¤é†’åŸå› å¹¶å°†å…¶èµ‹å€¼ç»™ `wakeup_reason` å˜é‡ã€‚

```cpp
  switch (wakeup_reason) {
    case ESP_SLEEP_WAKEUP_EXT0:     Serial.println("Wakeup caused by external signal using RTC_IO"); break;
    case ESP_SLEEP_WAKEUP_EXT1:     Serial.println("Wakeup caused by external signal using RTC_CNTL"); break;
    case ESP_SLEEP_WAKEUP_TIMER:    Serial.println("Wakeup caused by timer"); break;
    case ESP_SLEEP_WAKEUP_TOUCHPAD: Serial.println("Wakeup caused by touchpad"); break;
    case ESP_SLEEP_WAKEUP_ULP:      Serial.println("Wakeup caused by ULP program"); break;
    default:                        Serial.printf("Wakeup was not caused by deep sleep: %d\n", wakeup_reason); break;
}
```

- `ESP_SLEEP_WAKEUP_EXT0` : æ­¤å”¤é†’åŸå› è¡¨ç¤º ESP32 ç”±äºåœ¨é…ç½®ä¸º RTCï¼ˆå®æ—¶æ—¶é’Ÿï¼‰I/O çš„ GPIO å¼•è„šä¸Šæ£€æµ‹åˆ°å¤–éƒ¨ä¿¡å·è€Œå”¤é†’ã€‚è¿™é€šå¸¸ç”¨äºå½“æŒ‰é’®æˆ–ä¼ æ„Ÿå™¨è¢«è§¦å‘æ—¶ä»ç¡çœ ä¸­å”¤é†’ã€‚
- `ESP_SLEEP_WAKEUP_EXT1` : è¿™è¡¨ç¤ºå”¤é†’æ˜¯ç”± RTC æ§åˆ¶å™¨ç®¡ç†çš„ GPIO å¼•è„šä¸Šçš„å¤–éƒ¨ä¿¡å·å¼•èµ·çš„ã€‚ä¸ EXT0 ä¸åŒï¼ŒEXT1 å¯ä»¥å¤„ç†å¤šä¸ªå¼•è„šï¼Œå¹¶ä¸”å¯ä»¥åœ¨ä»»ä½•æŒ‡å®šå¼•è„šæ”¹å˜çŠ¶æ€æ—¶å”¤é†’ï¼ˆä¾‹å¦‚ï¼Œå˜ä¸ºä½ç”µå¹³æˆ–é«˜ç”µå¹³ï¼‰ã€‚
- `ESP_SLEEP_WAKEUP_TIMER` : æ­¤å”¤é†’åŸå› è¡¨ç¤º ESP32 åœ¨é¢„å®šä¹‰çš„å®šæ—¶å™¨æŒç»­æ—¶é—´åå”¤é†’ã€‚è¿™å¯¹äºéœ€è¦æ‰§è¡Œå‘¨æœŸæ€§ä»»åŠ¡è€Œä¸éœ€è¦ç”¨æˆ·äº¤äº’çš„åº”ç”¨ç¨‹åºå¾ˆæœ‰ç”¨ã€‚
- `ESP_SLEEP_WAKEUP_TOUCHPAD` : è¿™è¡¨ç¤º ESP32 ç”±äºè§¦æ‘¸æ¿äº‹ä»¶è€Œå”¤é†’ã€‚å¦‚æœé…ç½®ä¸ºå”¤é†’çš„è§¦æ‘¸æ¿æ£€æµ‹åˆ°è§¦æ‘¸ï¼Œå®ƒå¯ä»¥ä½¿è®¾å¤‡é€€å‡ºç¡çœ æ¨¡å¼ã€‚
- `ESP_SLEEP_WAKEUP_ULP` : æ­¤å”¤é†’åŸå› æ„å‘³ç€å”¤é†’æ˜¯ç”± ULPï¼ˆè¶…ä½åŠŸè€—ï¼‰ç¨‹åºè§¦å‘çš„ã€‚ULP å¯ä»¥åœ¨ä¸» CPU å¤„äºæ·±åº¦ç¡çœ æ—¶è¿è¡Œï¼Œå¹¶ä¸”å¯ä»¥åœ¨æ»¡è¶³æŸäº›æ¡ä»¶æ—¶å”¤é†’ ESP32ï¼Œä»è€Œå®ç°ä½åŠŸè€—æ“ä½œï¼Œæœ€å¤§é™åº¦åœ°å‡å°‘ç”µæ± æ¶ˆè€—ã€‚

```cpp
++bootCount;
```

- æ¯æ¬¡è®¾å¤‡é‡å¯æ—¶å¢åŠ å¯åŠ¨è®¡æ•°å¹¶æ‰“å°å‡ºæ¥ã€‚

```cpp
print_wakeup_reason();
```

- æ‰“å° ESP32 å”¤é†’çš„åŸå› ã€‚

```cpp
esp_sleep_enable_timer_wakeup(TIME_TO_SLEEP * uS_TO_S_FACTOR);
Serial.println("Setup ESP32 to sleep for every " + String(TIME_TO_SLEEP) + " Seconds");

Serial.println("Going to sleep now");
Serial.flush();
esp_deep_sleep_start();
Serial.println("This will never be printed");
```

- `esp_sleep_enable_timer_wakeup(TIME_TO_SLEEP * uS_TO_S_FACTOR);`å¯ç”¨å®šæ—¶å™¨åœ¨æŒ‡å®šæ—¶é—´åå”¤é†’ESP32ã€‚
- `Serial.flush();`ç¡®ä¿åœ¨è¿›å…¥ç¡çœ å‰å‘é€æ‰€æœ‰ä¸²å£æ•°æ®ã€‚
- `esp_deep_sleep_start();`å°†ESP32ç½®äºæ·±åº¦ç¡çœ æ¨¡å¼ã€‚

</TabItem>

<TabItem value="DeepSleepExample2" label="ExternalWakeUp" default>

```cpp

#include "driver/rtc_io.h"

#define BUTTON_PIN_BITMASK(GPIO) (1ULL << GPIO) 
#define USE_EXT0_WAKEUP          1              
#define WAKEUP_GPIO              GPIO_NUM_33   
RTC_DATA_ATTR int bootCount = 0;


void print_wakeup_reason() {
  esp_sleep_wakeup_cause_t wakeup_reason;

  wakeup_reason = esp_sleep_get_wakeup_cause();

  switch (wakeup_reason) {
    case ESP_SLEEP_WAKEUP_EXT0:     Serial.println("Wakeup caused by external signal using RTC_IO"); break;
    case ESP_SLEEP_WAKEUP_EXT1:     Serial.println("Wakeup caused by external signal using RTC_CNTL"); break;
    case ESP_SLEEP_WAKEUP_TIMER:    Serial.println("Wakeup caused by timer"); break;
    case ESP_SLEEP_WAKEUP_TOUCHPAD: Serial.println("Wakeup caused by touchpad"); break;
    case ESP_SLEEP_WAKEUP_ULP:      Serial.println("Wakeup caused by ULP program"); break;
    default:                        Serial.printf("Wakeup was not caused by deep sleep: %d\n", wakeup_reason); break;
  }
}

void setup() {
  Serial.begin(115200);
  delay(1000); 

  ++bootCount;
  Serial.println("Boot number: " + String(bootCount));

  print_wakeup_reason();

#if USE_EXT0_WAKEUP
  esp_sleep_enable_ext0_wakeup(WAKEUP_GPIO, 1); 

  rtc_gpio_pullup_dis(WAKEUP_GPIO);
  rtc_gpio_pulldown_en(WAKEUP_GPIO);

#else 
  esp_sleep_enable_ext1_wakeup_io(BUTTON_PIN_BITMASK(WAKEUP_GPIO), ESP_EXT1_WAKEUP_ANY_HIGH);

  rtc_gpio_pulldown_en(WAKEUP_GPIO); 
  rtc_gpio_pullup_dis(WAKEUP_GPIO);   
#endif

  Serial.println("Going to sleep now");
  esp_deep_sleep_start();
  Serial.println("This will never be printed");
}

void loop() {
}
```

### Detailed Notes

```cpp
#include "driver/rtc_io.h"
```

- åŒ…å« RTC I/O é©±åŠ¨ç¨‹åºä»¥è®¿é—® RTC GPIOã€‚

```cpp
#define BUTTON_PIN_BITMASK(GPIO) (1ULL << GPIO)  
#define USE_EXT0_WAKEUP          1              
#define WAKEUP_GPIO              GPIO_NUM_33    
RTC_DATA_ATTR int bootCount = 0;
```

- 2 ^ GPIO_NUMBER çš„åå…­è¿›åˆ¶è¡¨ç¤º
- 1 = EXT0å”¤é†’ï¼Œ0 = EXT1å”¤é†’
- ä»…å…è®¸RTC IO - ESP32å¼•è„šç¤ºä¾‹

```cpp
  switch (wakeup_reason) {
    case ESP_SLEEP_WAKEUP_EXT0:     Serial.println("Wakeup caused by external signal using RTC_IO"); break;
    case ESP_SLEEP_WAKEUP_EXT1:     Serial.println("Wakeup caused by external signal using RTC_CNTL"); break;
    case ESP_SLEEP_WAKEUP_TIMER:    Serial.println("Wakeup caused by timer"); break;
    case ESP_SLEEP_WAKEUP_TOUCHPAD: Serial.println("Wakeup caused by touchpad"); break;
    case ESP_SLEEP_WAKEUP_ULP:      Serial.println("Wakeup caused by ULP program"); break;
    default:                        Serial.printf("Wakeup was not caused by deep sleep: %d\n", wakeup_reason); break;
}
```

- `ESP_SLEEP_WAKEUP_EXT0` : æ­¤å”¤é†’åŸå› è¡¨ç¤º ESP32 ç”±äºåœ¨é…ç½®ä¸º RTCï¼ˆå®æ—¶æ—¶é’Ÿï¼‰I/O çš„ GPIO å¼•è„šä¸Šæ£€æµ‹åˆ°å¤–éƒ¨ä¿¡å·è€Œå”¤é†’ã€‚è¿™é€šå¸¸ç”¨äºå½“æŒ‰é’®æˆ–ä¼ æ„Ÿå™¨è¢«è§¦å‘æ—¶ä»ç¡çœ ä¸­å”¤é†’ã€‚
- `ESP_SLEEP_WAKEUP_EXT1` : è¿™è¡¨ç¤ºå”¤é†’æ˜¯ç”± RTC æ§åˆ¶å™¨ç®¡ç†çš„ GPIO å¼•è„šä¸Šçš„å¤–éƒ¨ä¿¡å·å¼•èµ·çš„ã€‚ä¸ EXT0 ä¸åŒï¼ŒEXT1 å¯ä»¥å¤„ç†å¤šä¸ªå¼•è„šï¼Œå¹¶ä¸”å¯ä»¥åœ¨ä»»ä½•æŒ‡å®šå¼•è„šæ”¹å˜çŠ¶æ€æ—¶å”¤é†’ï¼ˆä¾‹å¦‚ï¼Œå˜ä¸ºä½ç”µå¹³æˆ–é«˜ç”µå¹³ï¼‰ã€‚
- `ESP_SLEEP_WAKEUP_TIMER` : æ­¤å”¤é†’åŸå› è¡¨ç¤º ESP32 åœ¨é¢„å®šä¹‰çš„å®šæ—¶å™¨æŒç»­æ—¶é—´åå”¤é†’ã€‚è¿™å¯¹äºéœ€è¦æ‰§è¡Œå‘¨æœŸæ€§ä»»åŠ¡è€Œä¸éœ€è¦ç”¨æˆ·äº¤äº’çš„åº”ç”¨ç¨‹åºå¾ˆæœ‰ç”¨ã€‚
- `ESP_SLEEP_WAKEUP_TOUCHPAD` : è¿™è¡¨ç¤º ESP32 ç”±äºè§¦æ‘¸æ¿äº‹ä»¶è€Œå”¤é†’ã€‚å¦‚æœé…ç½®ä¸ºå”¤é†’çš„è§¦æ‘¸æ¿æ£€æµ‹åˆ°è§¦æ‘¸ï¼Œå®ƒå¯ä»¥ä½¿è®¾å¤‡é€€å‡ºç¡çœ æ¨¡å¼ã€‚
- `ESP_SLEEP_WAKEUP_ULP` : æ­¤å”¤é†’åŸå› æ„å‘³ç€å”¤é†’æ˜¯ç”± ULPï¼ˆè¶…ä½åŠŸè€—ï¼‰ç¨‹åºè§¦å‘çš„ã€‚ULP å¯ä»¥åœ¨ä¸» CPU å¤„äºæ·±åº¦ç¡çœ æ—¶è¿è¡Œï¼Œå¹¶ä¸”å¯ä»¥åœ¨æ»¡è¶³æŸäº›æ¡ä»¶æ—¶å”¤é†’ ESP32ï¼Œä»è€Œå®ç°ä½åŠŸè€—æ“ä½œï¼Œæœ€å¤§é™åº¦åœ°å‡å°‘ç”µæ± æ¶ˆè€—ã€‚

```cpp
  Serial.begin(115200);
  delay(1000);  
  ++bootCount;
  Serial.println("Boot number: " + String(bootCount));
  print_wakeup_reason();
```

- `++bootCount;` å¢åŠ å¯åŠ¨æ¬¡æ•°å¹¶åœ¨æ¯æ¬¡é‡å¯æ—¶æ‰“å°
- `print_wakeup_reason();` æ‰“å° ESP32 çš„å”¤é†’åŸå› 

```cpp
#if USE_EXT0_WAKEUP
  esp_sleep_enable_ext0_wakeup(WAKEUP_GPIO, 1);

  rtc_gpio_pullup_dis(WAKEUP_GPIO);
  rtc_gpio_pulldown_en(WAKEUP_GPIO);
```

- `esp_sleep_enable_ext0_wakeup(WAKEUP_GPIO, 1);`åœ¨æŒ‡å®šçš„GPIOå¼•è„šå˜ä¸ºé«˜ç”µå¹³æ—¶å¯ç”¨EXT0å”¤é†’ã€‚
- `rtc_gpio_pullup_dis(WAKEUP_GPIO);` ç¦ç”¨å”¤é†’GPIOå¼•è„šä¸Šçš„ä¸Šæ‹‰ç”µé˜»ã€‚
- `rtc_gpio_pulldown_en(WAKEUP_GPIO);` å¯ç”¨å”¤é†’GPIOå¼•è„šä¸Šçš„ä¸‹æ‹‰ç”µé˜»ã€‚

```cpp
#else  
  esp_sleep_enable_ext1_wakeup_io(BUTTON_PIN_BITMASK(WAKEUP_GPIO), ESP_EXT1_WAKEUP_ANY_HIGH);

  rtc_gpio_pulldown_en(WAKEUP_GPIO);  
  rtc_gpio_pullup_dis(WAKEUP_GPIO);   
```

- `esp_sleep_enable_ext1_wakeup_io(BUTTON_PIN_BITMASK(WAKEUP_GPIO), ESP_EXT1_WAKEUP_ANY_HIGH);`EXT1 å”¤é†’
- `rtc_gpio_pulldown_en(WAKEUP_GPIO);` GPIO33 è¿æ¥åˆ° GND ä»¥ä¾¿åœ¨é«˜ç”µå¹³æ—¶å”¤é†’
- `rtc_gpio_pullup_dis(WAKEUP_GPIO);`  ç¦ç”¨ä¸Šæ‹‰ä»¥å…è®¸åœ¨é«˜ç”µå¹³æ—¶å”¤é†’

- `esp_sleep_enable_ext1_wakeup_io(BUTTON_PIN_BITMASK(WAKEUP_GPIO), ESP_EXT1_WAKEUP_ANY_HIGH);`  å¦‚æœä½ è¦ä½¿ç”¨ ext1ï¼Œä½ å¯ä»¥è¿™æ ·ä½¿ç”¨
- `rtc_gpio_pulldown_en(WAKEUP_GPIO);` GPIO33 è¿æ¥åˆ° GND ä»¥ä¾¿åœ¨é«˜ç”µå¹³æ—¶å”¤é†’
- `rtc_gpio_pullup_dis(WAKEUP_GPIO);` ç¦ç”¨ä¸Šæ‹‰ä»¥å…è®¸åœ¨é«˜ç”µå¹³æ—¶å”¤é†’

```cpp
  Serial.println("Going to sleep now");
  esp_deep_sleep_start();
  Serial.println("This will never be printed");
```

- `esp_deep_sleep_start();`ä½¿ESP32è¿›å…¥æ·±åº¦ç¡çœ æ¨¡å¼ã€‚

</TabItem>

<TabItem value="DeepSleepExample3" label="TouchWakeUp" default>

```cpp
#if CONFIG_IDF_TARGET_ESP32
#define THRESHOLD 40 
#elif (CONFIG_IDF_TARGET_ESP32S2 || CONFIG_IDF_TARGET_ESP32S3)
#define THRESHOLD 5000 
#else                  
#define THRESHOLD 500  
#endif

RTC_DATA_ATTR int bootCount = 0;
touch_pad_t touchPin;

void print_wakeup_reason() {
  esp_sleep_wakeup_cause_t wakeup_reason;

  wakeup_reason = esp_sleep_get_wakeup_cause();

  switch (wakeup_reason) {
    case ESP_SLEEP_WAKEUP_EXT0:     Serial.println("Wakeup caused by external signal using RTC_IO"); break;
    case ESP_SLEEP_WAKEUP_EXT1:     Serial.println("Wakeup caused by external signal using RTC_CNTL"); break;
    case ESP_SLEEP_WAKEUP_TIMER:    Serial.println("Wakeup caused by timer"); break;
    case ESP_SLEEP_WAKEUP_TOUCHPAD: Serial.println("Wakeup caused by touchpad"); break;
    case ESP_SLEEP_WAKEUP_ULP:      Serial.println("Wakeup caused by ULP program"); break;
    default:                        Serial.printf("Wakeup was not caused by deep sleep: %d\n", wakeup_reason); break;
  }
}

void print_wakeup_touchpad() {
  touchPin = esp_sleep_get_touchpad_wakeup_status();

#if CONFIG_IDF_TARGET_ESP32
  switch (touchPin) {
    case 0:  Serial.println("Touch detected on GPIO 4"); break;
    case 1:  Serial.println("Touch detected on GPIO 0"); break;
    case 2:  Serial.println("Touch detected on GPIO 2"); break;
    case 3:  Serial.println("Touch detected on GPIO 15"); break;
    case 4:  Serial.println("Touch detected on GPIO 13"); break;
    case 5:  Serial.println("Touch detected on GPIO 12"); break;
    case 6:  Serial.println("Touch detected on GPIO 14"); break;
    case 7:  Serial.println("Touch detected on GPIO 27"); break;
    case 8:  Serial.println("Touch detected on GPIO 33"); break;
    case 9:  Serial.println("Touch detected on GPIO 32"); break;
    default: Serial.println("Wakeup not by touchpad"); break;
  }
#else
  if (touchPin < TOUCH_PAD_MAX) {
    Serial.printf("Touch detected on GPIO %d\n", touchPin);
  } else {
    Serial.println("Wakeup not by touchpad");
  }
#endif
}

void setup() {
  Serial.begin(115200);
  delay(1000); 

  ++bootCount;
  Serial.println("Boot number: " + String(bootCount));

  print_wakeup_reason();
  print_wakeup_touchpad();

#if CONFIG_IDF_TARGET_ESP32

  touchSleepWakeUpEnable(T3, THRESHOLD);
  touchSleepWakeUpEnable(T7, THRESHOLD);

#else 
  touchSleepWakeUpEnable(T3, THRESHOLD);

#endif

  Serial.println("Going to sleep now");
  esp_deep_sleep_start();
  Serial.println("This will never be printed");
}

void loop() {

}
```

### Detailed Notes

```cpp
#if CONFIG_IDF_TARGET_ESP32
#define THRESHOLD 40 
#elif (CONFIG_IDF_TARGET_ESP32S2 || CONFIG_IDF_TARGET_ESP32S3)
#define THRESHOLD 5000 
#else                  
#define THRESHOLD 500  
#endif
```

- æ£€æŸ¥ç›®æ ‡æ˜¯å¦ä¸º ESP32
- ä¸º ESP32 å®šä¹‰è§¦æ‘¸çµæ•åº¦é˜ˆå€¼
- æ£€æŸ¥ç›®æ ‡æ˜¯å¦ä¸º ESP32S2 æˆ– ESP32S3
- ä¸º ESP32S2/S3 å®šä¹‰æ›´é«˜çš„è§¦æ‘¸çµæ•åº¦é˜ˆå€¼
- å¦‚æœç›®æ ‡ä¸æ˜¯ä¸Šè¿°ä»»ä½•ä¸€ç§
- ä¸ºå…¶ä»–ç›®æ ‡å®šä¹‰é»˜è®¤é˜ˆå€¼

```cpp
RTC_DATA_ATTR int bootCount = 0; // Declare a variable to count boots, stored in RTC memory.
touch_pad_t touchPin; // Declare a variable to hold the touchpad pin status.

void print_wakeup_reason() { // Function to print the reason for waking up.
  esp_sleep_wakeup_cause_t wakeup_reason; // Variable to hold the wakeup reason.

  wakeup_reason = esp_sleep_get_wakeup_cause(); // Get the cause of the wakeup.
```

- `RTC_DATA_ATTR int bootCount = 0;` å£°æ˜ä¸€ä¸ªç”¨äºè®¡ç®—å¯åŠ¨æ¬¡æ•°çš„å˜é‡ï¼Œå­˜å‚¨åœ¨RTCå†…å­˜ä¸­ã€‚
- `touch_pad_t touchPin;` å£°æ˜ä¸€ä¸ªç”¨äºä¿å­˜è§¦æ‘¸æ¿å¼•è„šçŠ¶æ€çš„å˜é‡ã€‚
- `void print_wakeup_reason()` ç”¨äºæ‰“å°å”¤é†’åŸå› çš„å‡½æ•°ã€‚
- `esp_sleep_wakeup_cause_t wakeup_reason;` ç”¨äºä¿å­˜å”¤é†’åŸå› çš„å˜é‡ã€‚
- `wakeup_reason = esp_sleep_get_wakeup_cause();` è·å–å”¤é†’çš„åŸå› ã€‚

```cpp
  switch (wakeup_reason) {
    case ESP_SLEEP_WAKEUP_EXT0:     Serial.println("Wakeup caused by external signal using RTC_IO"); break;
    case ESP_SLEEP_WAKEUP_EXT1:     Serial.println("Wakeup caused by external signal using RTC_CNTL"); break;
    case ESP_SLEEP_WAKEUP_TIMER:    Serial.println("Wakeup caused by timer"); break;
    case ESP_SLEEP_WAKEUP_TOUCHPAD: Serial.println("Wakeup caused by touchpad"); break;
    case ESP_SLEEP_WAKEUP_ULP:      Serial.println("Wakeup caused by ULP program"); break;
    default:                        Serial.printf("Wakeup was not caused by deep sleep: %d\n", wakeup_reason); break;
}
```

- `ESP_SLEEP_WAKEUP_EXT0` : æ­¤å”¤é†’åŸå› è¡¨ç¤º ESP32 ç”±äºåœ¨é…ç½®ä¸º RTCï¼ˆå®æ—¶æ—¶é’Ÿï¼‰I/O çš„ GPIO å¼•è„šä¸Šæ£€æµ‹åˆ°å¤–éƒ¨ä¿¡å·è€Œå”¤é†’ã€‚è¿™é€šå¸¸ç”¨äºå½“æŒ‰é’®æˆ–ä¼ æ„Ÿå™¨è¢«è§¦å‘æ—¶ä»ç¡çœ ä¸­å”¤é†’ã€‚
- `ESP_SLEEP_WAKEUP_EXT1` : è¿™è¡¨ç¤ºå”¤é†’æ˜¯ç”± RTC æ§åˆ¶å™¨ç®¡ç†çš„ GPIO å¼•è„šä¸Šçš„å¤–éƒ¨ä¿¡å·å¼•èµ·çš„ã€‚ä¸ EXT0 ä¸åŒï¼ŒEXT1 å¯ä»¥å¤„ç†å¤šä¸ªå¼•è„šï¼Œå¹¶ä¸”å¯ä»¥åœ¨ä»»ä½•æŒ‡å®šå¼•è„šæ”¹å˜çŠ¶æ€æ—¶å”¤é†’ï¼ˆä¾‹å¦‚ï¼Œå˜ä¸ºä½ç”µå¹³æˆ–é«˜ç”µå¹³ï¼‰ã€‚
- `ESP_SLEEP_WAKEUP_TIMER` : æ­¤å”¤é†’åŸå› è¡¨ç¤º ESP32 åœ¨é¢„å®šä¹‰çš„å®šæ—¶å™¨æŒç»­æ—¶é—´åå”¤é†’ã€‚è¿™å¯¹äºéœ€è¦æ‰§è¡Œå‘¨æœŸæ€§ä»»åŠ¡è€Œä¸éœ€è¦ç”¨æˆ·äº¤äº’çš„åº”ç”¨ç¨‹åºå¾ˆæœ‰ç”¨ã€‚
- `ESP_SLEEP_WAKEUP_TOUCHPAD` : è¿™è¡¨ç¤º ESP32 ç”±äºè§¦æ‘¸æ¿äº‹ä»¶è€Œå”¤é†’ã€‚å¦‚æœé…ç½®ä¸ºå”¤é†’çš„è§¦æ‘¸æ¿æ£€æµ‹åˆ°è§¦æ‘¸ï¼Œå®ƒå¯ä»¥ä½¿è®¾å¤‡é€€å‡ºç¡çœ æ¨¡å¼ã€‚
- `ESP_SLEEP_WAKEUP_ULP` : æ­¤å”¤é†’åŸå› æ„å‘³ç€å”¤é†’æ˜¯ç”± ULPï¼ˆè¶…ä½åŠŸè€—ï¼‰ç¨‹åºè§¦å‘çš„ã€‚ULP å¯ä»¥åœ¨ä¸» CPU å¤„äºæ·±åº¦ç¡çœ æ—¶è¿è¡Œï¼Œå¹¶ä¸”å¯ä»¥åœ¨æ»¡è¶³æŸäº›æ¡ä»¶æ—¶å”¤é†’ ESP32ï¼Œä»è€Œå®ç°ä½åŠŸè€—æ“ä½œï¼Œæœ€å¤§é™åº¦åœ°å‡å°‘ç”µæ± æ¶ˆè€—ã€‚

```cpp
void print_wakeup_touchpad() {
  touchPin = esp_sleep_get_touchpad_wakeup_status();

#if CONFIG_IDF_TARGET_ESP32
  switch (touchPin) {
    case 0:  Serial.println("Touch detected on GPIO 4"); break;
    case 1:  Serial.println("Touch detected on GPIO 0"); break;
    case 2:  Serial.println("Touch detected on GPIO 2"); break;
    case 3:  Serial.println("Touch detected on GPIO 15"); break;
    case 4:  Serial.println("Touch detected on GPIO 13"); break;
    case 5:  Serial.println("Touch detected on GPIO 12"); break;
    case 6:  Serial.println("Touch detected on GPIO 14"); break;
    case 7:  Serial.println("Touch detected on GPIO 27"); break;
    case 8:  Serial.println("Touch detected on GPIO 33"); break;
    case 9:  Serial.println("Touch detected on GPIO 32"); break;
    default: Serial.println("Wakeup not by touchpad"); break;
  }
#else
  if (touchPin < TOUCH_PAD_MAX) {
    Serial.printf("Touch detected on GPIO %d\n", touchPin);
  } else {
    Serial.println("Wakeup not by touchpad");
  }
#endif
}
```

- `case 0:`åœ¨ GPIO 4 ä¸Šæ£€æµ‹åˆ°è§¦æ‘¸ã€‚
- `case 1:`åœ¨ GPIO 0 ä¸Šæ£€æµ‹åˆ°è§¦æ‘¸ã€‚
- `case 2:`åœ¨ GPIO 2 ä¸Šæ£€æµ‹åˆ°è§¦æ‘¸ã€‚
- `case 3:`åœ¨ GPIO 15 ä¸Šæ£€æµ‹åˆ°è§¦æ‘¸ã€‚
- `case 4:`åœ¨ GPIO 13 ä¸Šæ£€æµ‹åˆ°è§¦æ‘¸ã€‚
- `case 5:`åœ¨ GPIO 12 ä¸Šæ£€æµ‹åˆ°è§¦æ‘¸ã€‚
- `case 6:`åœ¨ GPIO 14 ä¸Šæ£€æµ‹åˆ°è§¦æ‘¸ã€‚
- `case 7:`åœ¨ GPIO 27 ä¸Šæ£€æµ‹åˆ°è§¦æ‘¸ã€‚
- `case 8:`åœ¨ GPIO 33 ä¸Šæ£€æµ‹åˆ°è§¦æ‘¸ã€‚
- `case 9:`åœ¨ GPIO 32 ä¸Šæ£€æµ‹åˆ°è§¦æ‘¸ã€‚
- `default:`å¦‚æœæ²¡æœ‰æ£€æµ‹åˆ°è§¦æ‘¸çš„é»˜è®¤æƒ…å†µã€‚

```cpp
void setup() {
  Serial.begin(115200);
  delay(1000); 

  ++bootCount;
  Serial.println("Boot number: " + String(bootCount));

  print_wakeup_reason();
  print_wakeup_touchpad();

#if CONFIG_IDF_TARGET_ESP32

  touchSleepWakeUpEnable(T3, THRESHOLD);
  touchSleepWakeUpEnable(T7, THRESHOLD);

#else 
  touchSleepWakeUpEnable(T3, THRESHOLD);

#endif

  Serial.println("Going to sleep now");
  esp_deep_sleep_start();
  Serial.println("This will never be printed");
}
```

- `++bootCount;` å¢åŠ å¯åŠ¨è®¡æ•°ã€‚

- `print_wakeup_reason();` æ‰“å°å”¤é†’åŸå› ã€‚
- `print_wakeup_touchpad();` æ‰“å°è§¦æ‘¸æ¿å”¤é†’çŠ¶æ€ã€‚

- `#if CONFIG_IDF_TARGET_ESP32` æ£€æŸ¥ç›®æ ‡æ˜¯å¦ä¸ºESP32
- `touchSleepWakeUpEnable(T3, THRESHOLD);` ä¸ºT3å¯ç”¨è§¦æ‘¸å”¤é†’ï¼Œä½¿ç”¨å®šä¹‰çš„é˜ˆå€¼ã€‚
- `touchSleepWakeUpEnable(T7, THRESHOLD);` ä¸ºT7å¯ç”¨è§¦æ‘¸å”¤é†’ï¼Œä½¿ç”¨å®šä¹‰çš„é˜ˆå€¼ã€‚
- `touchSleepWakeUpEnable(T3, THRESHOLD);` ä¸ºT3å¯ç”¨è§¦æ‘¸å”¤é†’ï¼Œä½¿ç”¨å®šä¹‰çš„é˜ˆå€¼ã€‚

- `esp_deep_sleep_start();` å°†ESP32ç½®äºæ·±åº¦ç¡çœ æ¨¡å¼ã€‚

</TabItem>

<TabItem value="DeepSleepExample4" label="SmoothBink_ULP" default>

```cpp
#include <Arduino.h> // Include the Arduino core library
#include "esp32/ulp.h" // Include ESP32 ULP-related library
#include "driver/rtc_io.h" // Include RTC GPIO driver library
#include "soc/rtc_io_reg.h" // Include RTC IO register definitions

#define RTC_dutyMeter 0 // Define the storage location for dutyMeter
#define RTC_dir       4 // Define the storage location for direction
#define RTC_fadeDelay 12 // Define the storage location for fadeDelay

uint32_t *fadeCycleDelay = &RTC_SLOW_MEM[RTC_fadeDelay]; // Point to the fadeDelay location in RTC_SLOW_MEM
#define ULP_START_OFFSET 32 // Define the starting offset for the ULP program

RTC_DATA_ATTR uint32_t ULP_Started = 0; // Variable to indicate if the ULP program has started
// Time-to-Sleep
#define uS_TO_S_FACTOR 1000000ULL // Conversion factor from microseconds to seconds
#define TIME_TO_SLEEP  5 // Time to enter deep sleep (in seconds)

void ulp_setup() { // ULP setup function
  if (ULP_Started) { // If ULP has already started, return
    return;
  }
  *fadeCycleDelay = 5; // Initialize fadeCycleDelay to 5
  ULP_Started = 1; // Mark ULP as started

  const gpio_num_t MeterPWMPin = GPIO_NUM_2; // Define the PWM pin
  rtc_gpio_init(MeterPWMPin); // Initialize GPIO
  rtc_gpio_set_direction(MeterPWMPin, RTC_GPIO_MODE_OUTPUT_ONLY); // Set the pin as output
  rtc_gpio_set_level(MeterPWMPin, 0); // Set the initial pin level to low

  const uint32_t MeterPWMBit = rtc_io_number_get(MeterPWMPin) + RTC_GPIO_OUT_DATA_S; // Get the bit for the PWM pin

  enum labels { // Define labels for the ULP program
    INIFINITE_LOOP,
    RUN_PWM,
    NEXT_PWM_CYCLE,
    PWM_ON,
    PWM_OFF,
    END_PWM_CYCLE,
    POSITIVE_DIR,
    DEC_DUTY,
    INC_DUTY,
  };

  // Define the ULP program
  const ulp_insn_t ulp_prog[] = {
    // Initial value setup
    I_MOVI(R0, 0), // Move 0 to register R0
    I_ST(R0, R0, RTC_dutyMeter), // Store the value of R0 in dutyMeter
    I_MOVI(R1, 1), // Move 1 to register R1
    I_ST(R1, R0, RTC_dir), // Store the value of R1 in dir

    M_LABEL(INIFINITE_LOOP), // Define the infinite loop label
    
    I_MOVI(R3, 0), // Move 0 to R3
    I_LD(R3, R3, RTC_fadeDelay), // Load the value from fadeDelay into R3
    M_LABEL(RUN_PWM), // Define the run PWM label

    I_MOVI(R0, 0), // Move 0 to R0
    I_LD(R0, R0, RTC_dutyMeter), // Load the value from dutyMeter into R0
    M_BL(NEXT_PWM_CYCLE, 1), // Branch to the next PWM cycle
    I_WR_REG(RTC_GPIO_OUT_W1TS_REG, MeterPWMBit, MeterPWMBit, 1), // Set the PWM pin high
    M_LABEL(PWM_ON), // Define the PWM ON label
    M_BL(NEXT_PWM_CYCLE, 1), // Branch to the next PWM cycle
    // I_DELAY(8), // Commented out delay instruction
    I_SUBI(R0, R0, 1), // Decrement R0 by 1
    M_BX(PWM_ON), // Go back to the PWM ON label

    M_LABEL(NEXT_PWM_CYCLE), // Define the next PWM cycle label
    I_MOVI(R0, 0), // Move 0 to R0
    I_LD(R0, R0, RTC_dutyMeter), // Load the value from dutyMeter into R0
    I_MOVI(R1, 100), // Move 100 to R1
    I_SUBR(R0, R1, R0), // R0 = R1 - R0
    M_BL(END_PWM_CYCLE, 1), // Branch to the end PWM cycle label
    I_WR_REG(RTC_GPIO_OUT_W1TC_REG, MeterPWMBit, MeterPWMBit, 1), // Set the PWM pin low
    M_LABEL(PWM_OFF), // Define the PWM OFF label
    M_BL(END_PWM_CYCLE, 1), // Branch to the end PWM cycle label
    // I_DELAY(8), // Commented out delay instruction
    I_SUBI(R0, R0, 1), // Decrement R0 by 1
    M_BX(PWM_OFF), // Go back to the PWM OFF label
    M_LABEL(END_PWM_CYCLE), // Define the end PWM cycle label

    I_SUBI(R3, R3, 1), // Decrement R3 by 1
    I_MOVR(R0, R3), // Move R3 to R0
    M_BGE(RUN_PWM, 1), // If R3 >= 0, branch to RUN_PWM

    I_MOVI(R1, 0), // Move 0 to R1
    I_LD(R1, R1, RTC_dutyMeter), // Load the value from dutyMeter into R1
    I_MOVI(R0, 0), // Move 0 to R0
    I_LD(R0, R0, RTC_dir), // Load the value from dir into R0

    M_BGE(POSITIVE_DIR, 1), // If R0 >= 0, branch to POSITIVE_DIR

    I_MOVR(R0, R1), // Move R1 to R0
    M_BGE(DEC_DUTY, 1), // If R1 >= 0, branch to DEC_DUTY
    I_MOVI(R3, 0), // Move 0 to R3
    I_MOVI(R2, 1), // Move 1 to R2
    I_ST(R2, R3, RTC_dir), // Store the value of R2 in dir
    M_BX(INC_DUTY), // Branch to INC_DUTY label
    M_LABEL(DEC_DUTY), // Define DEC_DUTY label
    I_SUBI(R0, R0, 2), // Decrement R0 by 2
    I_MOVI(R2, 0), // Move 0 to R2
    I_ST(R0, R2, RTC_dutyMeter), // Store the value of R0 in dutyMeter
    M_BX(INIFINITE_LOOP), // Go back to the infinite loop label

    M_LABEL(POSITIVE_DIR), // Define POSITIVE_DIR label
  
    I_MOVR(R0, R1), // Move R1 to R0
    M_BL(INC_DUTY, 100), // Branch to INC_DUTY label with parameter 100
    I_MOVI(R2, 0), // Move 0 to R2
    I_ST(R2, R2, RTC_dir), // Store the value of R2 in dir
    M_BX(DEC_DUTY), // Branch to DEC_DUTY label
    M_LABEL(INC_DUTY), // Define INC_DUTY label
    I_ADDI(R0, R0, 2), // Increment R0 by 2
    I_MOVI(R2, 0), // Move 0 to R2
    I_ST(R0, R2, RTC_dutyMeter), // Store the value of R0 in dutyMeter
  
    M_BX(INIFINITE_LOOP), // Go back to the infinite loop label
  };
  // Run the ULP program
  size_t size = sizeof(ulp_prog) / sizeof(ulp_insn_t); // Calculate the size of the ULP program
  ulp_process_macros_and_load(ULP_START_OFFSET, ulp_prog, &size); // Load the ULP program
  esp_sleep_pd_config(ESP_PD_DOMAIN_RTC_PERIPH, ESP_PD_OPTION_ON); // Configure power management for RTC peripherals
  ulp_run(ULP_START_OFFSET); // Start the ULP program
}

void setup() { // Arduino setup function
  Serial.begin(115200); // Initialize serial communication at 115200 baud rate

  ulp_setup(); // Call the ULP setup function
  Serial.printf("\nStarted smooth blink with delay %ld\n", *fadeCycleDelay); // Print startup information

  if (*fadeCycleDelay < 195) { // If fadeCycleDelay is less than 195
    *fadeCycleDelay += 10; // Increase fadeCycleDelay
  } else {
    *fadeCycleDelay = 5; // Otherwise, reset fadeCycleDelay to 5
  }
  Serial.println("Entering in Deep Sleep"); // Print entering deep sleep information
  esp_sleep_enable_timer_wakeup(TIME_TO_SLEEP * uS_TO_S_FACTOR /*/ 4*/); // Set timer wakeup
  esp_deep_sleep_start(); // Enter deep sleep
}

void loop() { // Arduino loop function
  // Empty loop
}
```

</TabItem>

</Tabs>

:::tip
è¦åœ¨è¿›å…¥æ·±åº¦ç¡çœ æ¨¡å¼åé‡æ–°çƒ§å½•ç¨‹åºï¼Œè¯·æŒ‰ä½å¯åŠ¨æŒ‰é’®ï¼Œç„¶åæŒ‰ä¸‹å¤ä½æŒ‰é’®æ¥é‡å¯ESP32ã€‚
:::

### ç»“æœå±•ç¤º

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/ESP32S3_Sense_SleepMode/deep.png" style={{width:700, height:'auto'}}/></div>

## è½»åº¦ç¡çœ 

### ä»‹ç»

è½»åº¦ç¡çœ æ¨¡å¼æ˜¯ESP32ä¸­çš„å¦ä¸€ç§ä½åŠŸè€—æ¨¡å¼ï¼Œå…è®¸è®¾å¤‡åœ¨èŠ‚çº¦èƒ½æºçš„åŒæ—¶ä»ä¿æŒå¿«é€Ÿå“åº”æ—¶é—´ã€‚åœ¨æ­¤æ¨¡å¼ä¸‹ï¼ŒCPUå†…æ ¸è¢«æš‚åœï¼Œä½†RAMå’Œä¸€äº›å¤–è®¾ä»ä¿æŒä¾›ç”µï¼Œä½¿è®¾å¤‡èƒ½å¤Ÿå¿«é€Ÿå“åº”æŸäº›äº‹ä»¶è€Œå”¤é†’ã€‚

è½»åº¦ç¡çœ éå¸¸é€‚åˆéœ€è¦ä½åŠŸè€—ä½†ä»éœ€è¦ä¿æŒWiFiæˆ–è“ç‰™è¿æ¥çš„åº”ç”¨ï¼Œå› ä¸ºå®ƒå…è®¸æ— çº¿é€šä¿¡æ¨¡å—ä¿æŒæ´»è·ƒçŠ¶æ€ã€‚

### å”¤é†’æ–¹æ³•

- **å®šæ—¶å™¨å”¤é†’ï¼š** è®¾å¤‡å¯ä»¥åœ¨æŒ‡å®šæ—¶é—´æ®µåå”¤é†’ï¼Œå…è®¸æ‰§è¡Œå‘¨æœŸæ€§ä»»åŠ¡ã€‚
- **å¤–éƒ¨ä¸­æ–­å”¤é†’ï¼š** ESP32å¯ä»¥é€šè¿‡å¤–éƒ¨ä¿¡å·å”¤é†’ï¼Œå¦‚æŒ‰é’®æŒ‰å‹æˆ–å…¶ä»–ç¡¬ä»¶ä¸­æ–­ã€‚
- **ç½‘ç»œæ´»åŠ¨å”¤é†’ï¼š** è®¾å¤‡å¯ä»¥å“åº”ä¼ å…¥çš„ç½‘ç»œæ•°æ®åŒ…è€Œå”¤é†’ï¼Œå®ç°é«˜æ•ˆé€šä¿¡è€Œæ— éœ€æŒç»­å¤„äºæ´»è·ƒçŠ¶æ€ã€‚
- **GPIOå”¤é†’ï¼š** ç‰¹å®šçš„GPIOå¼•è„šå¯ä»¥é…ç½®ä¸ºåœ¨äº‹ä»¶å‘ç”Ÿæ—¶ä»è½»åº¦ç¡çœ ä¸­å”¤é†’è®¾å¤‡ï¼Œå¦‚çŠ¶æ€å˜åŒ–æˆ–ä¿¡å·ã€‚

### ä»£ç å®ç°

```cpp
#include <freertos/FreeRTOS.h>
#include <freertos/task.h>

const int sleepTime = 10000;
const int ledPin = LED_BUILTIN; 

void ledTask(void *pvParameters) {
  digitalWrite(ledPin, HIGH);
  Serial.println("LED is ON");
  vTaskDelay(pdMS_TO_TICKS(1000));
  digitalWrite(ledPin, LOW);
  Serial.println("LED is OFF");
  
  vTaskDelete(NULL); 
}

void setup() {
  Serial.begin(115200);
  pinMode(ledPin, OUTPUT);
  Serial.println("Setup complete. Going to sleep...");
}

void loop() {
  esp_sleep_enable_timer_wakeup(sleepTime * 1000);
  Serial.println("Going to sleep now...");
  esp_light_sleep_start();

  xTaskCreate(ledTask, "LED Task", 2048, NULL, 1, NULL);
  
  delay(1000);
}
```

### Detailed Notes

```cpp
#include <freertos/FreeRTOS.h>
#include <freertos/task.h>
```

- Incldue FreeRTOS library

```cpp
const int sleepTime = 10000; 
const int ledPin = LED_BUILTIN; 
```

- å°†ç¡çœ æ—¶é—´è®¾ç½®ä¸º 10 ç§’
- ä½¿ç”¨å†…ç½®çš„ LED å¼•è„š

```cpp
void ledTask(void *pvParameters): 
```

- å®šä¹‰ä¸€ä¸ª FreeRTOS ä»»åŠ¡æ¥æ§åˆ¶ LED çŠ¶æ€ã€‚

```cpp
digitalWrite(ledPin, HIGH); 
Serial.println("LED is ON"); 
vTaskDelay(pdMS_TO_TICKS(1000)); 
digitalWrite(ledPin, LOW);
Serial.println("LED is OFF"); 
vTaskDelete(NULL); 
```

- `vTaskDelay(pdMS_TO_TICKS(1000));`ä¿æŒLEDå¼€å¯1ç§’é’Ÿ
- `vTaskDelete(NULL);`åˆ é™¤å½“å‰ä»»åŠ¡

```cpp
esp_sleep_enable_timer_wakeup(sleepTime * 1000);
Serial.println("Going to sleep now..."); 
esp_light_sleep_start(); 
xTaskCreate(ledTask, "LED Task", 2048, NULL, 1, NULL); 
delay(1000); 
```

- `esp_sleep_enable_timer_wakeup(sleepTime * 1000);`è®¾ç½®å”¤é†’å®šæ—¶å™¨
- `esp_light_sleep_start();` è¿›å…¥è½»åº¦ç¡çœ æ¨¡å¼
- `xTaskCreate(ledTask, "LED Task", 2048, NULL, 1, NULL);`åˆ›å»ºLEDæ§åˆ¶ä»»åŠ¡

### ç»“æœå±•ç¤º

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/ESP32S3_Sense_SleepMode/light_led.gif" style={{width:300, height:'auto'}}/></div>

## è°ƒåˆ¶è§£è°ƒå™¨ç¡çœ æ¨¡å¼

### ä»‹ç»

è°ƒåˆ¶è§£è°ƒå™¨ç¡çœ æ¨¡å¼æ˜¯ESP32ä¸­å¦ä¸€ä¸ªé‡è¦çš„ä½åŠŸè€—æ¨¡å¼ï¼Œå®ƒä¸æ·±åº¦ç¡çœ æ¨¡å¼ä¸åŒã€‚è°ƒåˆ¶è§£è°ƒå™¨ç¡çœ æ¨¡å¼ä¸»è¦é’ˆå¯¹ESP32çš„æ— çº¿é€šä¿¡æ¨¡å—è¿›è¡Œä¼˜åŒ–ã€‚

åœ¨æ­¤æ¨¡å¼ä¸‹ï¼ŒESP32çš„WiFi/è“ç‰™æ¨¡å—è¿›å…¥ç¡çœ çŠ¶æ€ï¼Œè€ŒCPUæ ¸å¿ƒä¿æŒæ´»è·ƒã€‚è¿™ä½¿å¾—ESP32èƒ½å¤Ÿåœ¨æ˜¾è‘—é™ä½åŠŸè€—çš„åŒæ—¶ä¿æŒä¸€å®šç¨‹åº¦çš„æ— çº¿è¿æ¥èƒ½åŠ›ã€‚

### å”¤é†’æ–¹æ³•

- å®šæ—¶å™¨å”¤é†’

- å¤–éƒ¨ä¸­æ–­å”¤é†’

- ä»»åŠ¡å”¤é†’

- ç½‘ç»œæ´»åŠ¨å”¤é†’

### ä»£ç å®ç°

```cpp
#include "WiFi.h"

void setup() {
    Serial.begin(115200);
    Serial.println("Connecting to WiFi...");

    WiFi.begin("****", "****");

    while (WiFi.status() != WL_CONNECTED) {
        delay(1000);
        Serial.println("Connecting...");
    }
    Serial.println("Connected to WiFi!");
 
    WiFi.setSleep(true); 
    Serial.println("Modem-Sleep enabled.");
}

void loop() {

    Serial.println("Running...");

    delay(5000);

    WiFi.setSleep(false); 
    Serial.println("Modem-Sleep disabled. WiFi is active.");

    if (WiFi.status() == WL_CONNECTED) {
        Serial.println("Still connected to WiFi.");
    } else {
        Serial.println("WiFi disconnected.");
    }

    delay(5000);
    WiFi.setSleep(true); 
    Serial.println("Modem-Sleep enabled.");
}
```

### Detailed Notes

```cpp
#include "WiFi.h"
```

- åŒ…å« WiFi åº“ä»¥å¯ç”¨ WiFi å‡½æ•°ã€‚

```cpp
Serial.println("Connecting to WiFi...");
```

- æ‰“å°ä¸€æ¡æ¶ˆæ¯ï¼ŒæŒ‡ç¤ºæ­£åœ¨å¼€å§‹è¿æ¥åˆ° WiFiã€‚

```cpp
WiFi.begin("****", "****");
```

- å¼€å§‹è¿æ¥åˆ°æŒ‡å®šçš„ WiFi ç½‘ç»œã€‚

```cpp
    while (WiFi.status() != WL_CONNECTED) {
        delay(1000);
        Serial.println("Connecting...");
    }
    Serial.println("Connected to WiFi!");
```

- å¾ªç¯ç›´åˆ°æˆåŠŸè¿æ¥åˆ° WiFiã€‚

```cpp
WiFi.setSleep(true);
```

- å¯ç”¨è°ƒåˆ¶è§£è°ƒå™¨ç¡çœ æ¨¡å¼ä»¥èŠ‚çœç”µé‡ã€‚

```cpp
WiFi.setSleep(false);
```

- ç¦ç”¨è°ƒåˆ¶è§£è°ƒå™¨ç¡çœ æ¨¡å¼ä»¥æ¿€æ´» WiFiã€‚

```cpp
if (WiFi.status() == WL_CONNECTED) {
```

- Check the WiFi status.

```cpp
WiFi.setSleep(true);
```

- é‡æ–°å¯ç”¨è°ƒåˆ¶è§£è°ƒå™¨ç¡çœ æ¨¡å¼ã€‚

### ç»“æœå±•ç¤º

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/ESP32S3_Sense_SleepMode/light.png" style={{width:700, height:'auto'}}/></div>

## ç¡çœ åŠŸèƒ½åº”ç”¨

é€šè¿‡ä¸Šé¢çš„ç®€å•ç¤ºä¾‹ï¼Œç°åœ¨è®©æˆ‘ä»¬æ›´è¿›ä¸€æ­¥ï¼Œåœ¨ ESP32 S3 Sense ä¼ æ„Ÿå™¨ä¸Šä½¿ç”¨è¿™äº›ç¡çœ åŠŸèƒ½ã€‚

### è½¯ä»¶å‡†å¤‡

åœ¨å¼€å§‹æœ¬æ–‡ä¹‹å‰ï¼Œå¦‚æœæ‚¨è¿˜æ²¡æœ‰ä½¿ç”¨ XIAO ESP32S3 Sense ä¸Šçš„æ‰€æœ‰ç¡¬ä»¶åŠŸèƒ½ï¼Œè¯·ç¡®ä¿æ‚¨å·²ç»å®Œæˆäº†ä¸€äº›è½¯ä»¶å®‰è£…å‡†å¤‡å·¥ä½œã€‚

ä»¥ä¸‹æ˜¯ä¸‰ä¸ªåŠŸèƒ½çš„ä»‹ç»ï¼Œæ‚¨å¯ä»¥é€šè¿‡ä»¥ä¸‹é“¾æ¥æ‰¾åˆ°æ›´å¤šä¿¡æ¯ï¼š

- [éº¦å…‹é£ä½¿ç”¨](https://wiki.seeedstudio.com/cn/xiao_esp32s3_sense_mic/)ï¼šå­¦ä¹ å¦‚ä½•ä½¿ç”¨ XIAO ESP32S3 Sense ä¸Šçš„éº¦å…‹é£æ¥æ•è·ç¯å¢ƒå£°éŸ³çº§åˆ«å¹¶å½•åˆ¶éŸ³é¢‘ã€‚

- [MicroSD](https://wiki.seeedstudio.com/cn/xiao_esp32s3_sense_filesystem/)ï¼šäº†è§£å¦‚ä½•ä½¿ç”¨ MicroSD å¡è¿›è¡Œæ•°æ®å­˜å‚¨ï¼Œç¡®ä¿æ‚¨å¯ä»¥åœ¨é¡¹ç›®ä¸­ä¿å­˜å’Œæ£€ç´¢æ–‡ä»¶ã€‚

- [ç›¸æœºä½¿ç”¨](https://wiki.seeedstudio.com/cn/xiao_esp32s3_camera_usage/)ï¼šæŒæ¡å¦‚ä½•ä½¿ç”¨ XIAO ESP32S3 Sense ä¸Šçš„ç›¸æœºæ¨¡å—æ‹ç…§å’Œå½•åˆ¶è§†é¢‘ã€‚

### ä»£ç å®ç°

<Tabs>
<TabItem  value="SleepApplication1" label="Deep-Sleep" default>

```cpp
#include "esp_camera.h"
#include "FS.h"
#include "SD.h"
#include "SPI.h"

#define CAMERA_MODEL_XIAO_ESP32S3 

#include "camera_pins.h"

unsigned long lastCaptureTime = 0; 
int imageCount = 1;                
bool camera_sign = false;        
bool sd_sign = false;             


void photo_save(const char * fileName) {
  camera_fb_t *fb = esp_camera_fb_get();
  if (!fb) {
    Serial.println("Failed to get camera frame buffer");
    return;
  }
  writeFile(SD, fileName, fb->buf, fb->len);
  
  esp_camera_fb_return(fb);

  Serial.println("Photo saved to file");
}

void writeFile(fs::FS &fs, const char * path, uint8_t * data, size_t len){
    Serial.printf("Writing file: %s\r\n", path);

    File file = fs.open(path, FILE_WRITE);
    if (!file) {
        Serial.println("Failed to open file for writing");
        return;
    }
    if (file.write(data, len) == len) {
        Serial.println("File written");
    } else {
        Serial.println("Write failed");
    }
    file.close();
}

void setup() {
  Serial.begin(115200);
  while (!Serial); 

  camera_config_t config;
  config.ledc_channel = LEDC_CHANNEL_0;
  config.ledc_timer = LEDC_TIMER_0;
  config.pin_d0 = Y2_GPIO_NUM;
  config.pin_d1 = Y3_GPIO_NUM;
  config.pin_d2 = Y4_GPIO_NUM;
  config.pin_d3 = Y5_GPIO_NUM;
  config.pin_d4 = Y6_GPIO_NUM;
  config.pin_d5 = Y7_GPIO_NUM;
  config.pin_d6 = Y8_GPIO_NUM;
  config.pin_d7 = Y9_GPIO_NUM;
  config.pin_xclk = XCLK_GPIO_NUM;
  config.pin_pclk = PCLK_GPIO_NUM;
  config.pin_vsync = VSYNC_GPIO_NUM;
  config.pin_href = HREF_GPIO_NUM;
  config.pin_sscb_sda = SIOD_GPIO_NUM;
  config.pin_sscb_scl = SIOC_GPIO_NUM;
  config.pin_pwdn = PWDN_GPIO_NUM;
  config.pin_reset = RESET_GPIO_NUM;
  config.xclk_freq_hz = 20000000;
  config.frame_size = FRAMESIZE_UXGA;
  config.pixel_format = PIXFORMAT_JPEG; 
  config.grab_mode = CAMERA_GRAB_WHEN_EMPTY;
  config.fb_location = CAMERA_FB_IN_PSRAM;
  config.jpeg_quality = 12;
  config.fb_count = 1;

  esp_err_t err = esp_camera_init(&config);
  if (err != ESP_OK) {
    Serial.printf("Camera init failed with error 0x%x", err);
    return;
  }

  camera_sign = true; 

  if (!SD.begin(21)) {
    Serial.println("Card Mount Failed");
    return;
  }
  
  uint8_t cardType = SD.cardType();
  
  if (cardType == CARD_NONE) {
    Serial.println("No SD card attached");
    return;
  }

  Serial.print("SD Card Type: ");
  if (cardType == CARD_MMC) {
    Serial.println("MMC");
  } else if (cardType == CARD_SD) {
    Serial.println("SDSC");
  } else if (cardType == CARD_SDHC) {
    Serial.println("SDHC");
  } else {
    Serial.println("UNKNOWN");
  }

  sd_sign = true; 

  Serial.println("Photos will begin shortly, please be ready.");
}

void loop() {
  if (camera_sign && sd_sign) {
    unsigned long now = millis();
  
    if ((now - lastCaptureTime) >= 60000) {
      char filename[32];
      sprintf(filename, "/image%d.jpg", imageCount);
      photo_save(filename);
      Serial.printf("Saved picture: %s\r\n", filename);
      Serial.println("Entering deep sleep for 10 seconds...");
      
      esp_sleep_enable_timer_wakeup(10000000); 
      esp_deep_sleep_start(); 
      
    }
  }
}
```

### è¯¦ç»†è¯´æ˜

è¿™æ®µä»£ç å®ç°äº†ä¸€ä¸ªåŸºäºESP32æ‘„åƒå¤´æ¨¡å—çš„å›¾åƒæ•è·ç³»ç»Ÿï¼Œå¯ä»¥æ¯60ç§’è‡ªåŠ¨æ‹æ‘„ä¸€å¼ ç…§ç‰‡å¹¶ä¿å­˜åˆ°SDå¡ä¸­ã€‚åœ¨`void setup()`å‡½æ•°ä¸­ï¼Œåˆå§‹åŒ–æ‘„åƒå¤´å’ŒSDå¡å¹¶ç¡®è®¤è®¾å¤‡çŠ¶æ€ï¼›åœ¨`void loop()`å‡½æ•°ä¸­ï¼Œæ£€æŸ¥æ‘„åƒå¤´æ˜¯å¦å¯ä»¥æ‹ç…§ï¼Œå¦‚æœæ»¡è¶³æ¡ä»¶ï¼Œåˆ™è°ƒç”¨`photo_save()`å‡½æ•°ä¿å­˜å›¾åƒï¼Œå¹¶åœ¨ä¿å­˜åè¿›å…¥æ·±åº¦ç¡çœ çŠ¶æ€10ç§’ä»¥èŠ‚çœèƒ½è€—ã€‚

</TabItem>

<TabItem  value="SleepApplication2" label="Light-Sleep" default>

```cpp
#include <ESP_I2S.h>
#include <freertos/FreeRTOS.h>
#include <freertos/task.h>

I2SClass I2S;

const int sleepTime = 10000;

void i2sTask(void *pvParameters) {
  Serial.println("start collecting");
  for (int i = 0; i < 10; i++) {
    int sample = I2S.read();
    if (sample && sample != -1 && sample != 1) {
      Serial.println(sample);
    }
    vTaskDelay(pdMS_TO_TICKS(1000));
  }
  vTaskDelay(pdMS_TO_TICKS(3000));
  vTaskDelete(NULL);
}

void setup() {
  Serial.begin(115200);
  while (!Serial) {
    ;
  }

  I2S.setPinsPdmRx(42, 41);

  if (!I2S.begin(I2S_MODE_PDM_RX, 16000, I2S_DATA_BIT_WIDTH_16BIT, I2S_SLOT_MODE_MONO)) {
    Serial.println("Failed to initialize I2S!");
    while (1);
  }
}

void loop() {
  esp_sleep_enable_timer_wakeup(sleepTime * 1000);
  xTaskCreate(i2sTask, "I2S Task", 2048, NULL, 1, NULL);
  
  Serial.println("Going to sleep now...");
  esp_light_sleep_start();

  delay(1000);
}
```

### è¯¦ç»†è¯´æ˜

æ­¤ä»£ç å®ç°äº†ä½¿ç”¨ I2S æ¥å£æ•è·éŸ³é¢‘æ•°æ®çš„åŠŸèƒ½ã€‚åœ¨ `void setup()` å‡½æ•°ä¸­ï¼Œåˆå§‹åŒ–äº†ä¸²å£å’Œ I2S æ¥å£ï¼›åœ¨ `void loop()` å‡½æ•°ä¸­ï¼Œå¯ç”¨äº†å”¤é†’å®šæ—¶å™¨å¹¶åˆ›å»ºäº†ä¸€ä¸ªä»»åŠ¡ `void i2sTask(void *pvParameters)`ï¼Œè¯¥ä»»åŠ¡è´Ÿè´£è¯»å–éŸ³é¢‘é‡‡æ ·å¹¶æ¯ç§’æ‰“å°æœ‰æ•ˆæ•°æ®ã€‚ä»»åŠ¡è¿è¡Œ 10 æ¬¡åï¼Œå»¶è¿Ÿ 3 ç§’å¹¶åˆ é™¤è‡ªèº«ã€‚

</TabItem>

<TabItem  value="SleepApplication3" label="Modem-Sleep" default>

```cpp
#include "esp_camera.h"
#include <WiFi.h>

#define CAMERA_MODEL_XIAO_ESP32S3
#include "camera_pins.h"

const char *ssid = "******";
const char *password = "******";

void startCameraServer();
void setupLedFlash(int pin);

unsigned long lastCameraOperationTime = 0;
const unsigned long sleepDelay = 10000;

void setup() {
  Serial.begin(115200);
  Serial.setDebugOutput(true);
  Serial.println();

  camera_config_t config;
  config.ledc_channel = LEDC_CHANNEL_0;
  config.ledc_timer = LEDC_TIMER_0;
  config.pin_d0 = Y2_GPIO_NUM;
  config.pin_d1 = Y3_GPIO_NUM;
  config.pin_d2 = Y4_GPIO_NUM;
  config.pin_d3 = Y5_GPIO_NUM;
  config.pin_d4 = Y6_GPIO_NUM;
  config.pin_d5 = Y7_GPIO_NUM;
  config.pin_d6 = Y8_GPIO_NUM;
  config.pin_d7 = Y9_GPIO_NUM;
  config.pin_xclk = XCLK_GPIO_NUM;
  config.pin_pclk = PCLK_GPIO_NUM;
  config.pin_vsync = VSYNC_GPIO_NUM;
  config.pin_href = HREF_GPIO_NUM;
  config.pin_sccb_sda = SIOD_GPIO_NUM;
  config.pin_sccb_scl = SIOC_GPIO_NUM;
  config.pin_pwdn = PWDN_GPIO_NUM;
  config.pin_reset = RESET_GPIO_NUM;
  config.xclk_freq_hz = 20000000;
  config.frame_size = FRAMESIZE_UXGA;
  config.pixel_format = PIXFORMAT_JPEG;
  config.grab_mode = CAMERA_GRAB_WHEN_EMPTY;
  config.fb_location = CAMERA_FB_IN_PSRAM;
  config.jpeg_quality = 12;
  config.fb_count = 1;

  if (config.pixel_format == PIXFORMAT_JPEG) {
    if (psramFound()) {
      config.jpeg_quality = 10;
      config.fb_count = 2;
      config.grab_mode = CAMERA_GRAB_LATEST;
    } else {
      config.frame_size = FRAMESIZE_SVGA;
      config.fb_location = CAMERA_FB_IN_DRAM;
    }
  } else {
    config.frame_size = FRAMESIZE_240X240;
#if CONFIG_IDF_TARGET_ESP32S3
    config.fb_count = 2;
#endif
  }

#if defined(CAMERA_MODEL_ESP_EYE)
  pinMode(13, INPUT_PULLUP);
  pinMode(14, INPUT_PULLUP);
#endif

  esp_err_t err = esp_camera_init(&config);
  if (err != ESP_OK) {
    Serial.printf("Camera init failed with error 0x%x", err);
    return;
  }

  sensor_t *s = esp_camera_sensor_get();
  if (s->id.PID == OV3660_PID) {
    s->set_vflip(s, 1);
    s->set_brightness(s, 1);
    s->set_saturation(s, -2);
  }
  if (config.pixel_format == PIXFORMAT_JPEG) {
    s->set_framesize(s, FRAMESIZE_QVGA);
  }

#if defined(CAMERA_MODEL_M5STACK_WIDE) || defined(CAMERA_MODEL_M5STACK_ESP32CAM)
  s->set_vflip(s, 1);
  s->set_hmirror(s, 1);
#endif

#if defined(CAMERA_MODEL_ESP32S3_EYE)
  s->set_vflip(s, 1);
#endif

#if defined(LED_GPIO_NUM)
  setupLedFlash(LED_GPIO_NUM);
#endif

  WiFi.begin(ssid, password);
  WiFi.setSleep(false);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("");
  Serial.println("WiFi connected");

  startCameraServer();

  Serial.print("Camera Ready! Use 'http://");
  Serial.print(WiFi.localIP());
  Serial.println("' to connect");
}

void loop() {
  delay(10000);

  if (WiFi.getSleep()) {
    Serial.println("WiFi is in sleep mode.");
  } else {
    Serial.println("WiFi is active.");
  }

  if (millis() - lastCameraOperationTime > sleepDelay) {
    WiFi.setSleep(true);
    Serial.println("No camera operation. WiFi is now in sleep mode.");
  } else {
    WiFi.setSleep(false);
  }
  cameraOperation();
}

void cameraOperation() {
  lastCameraOperationTime = millis();
}
```

### è¯¦ç»†è¯´æ˜

è¿™æ®µä»£ç å®ç°äº†ä½¿ç”¨ ESP32 æ‘„åƒå¤´æ¨¡å—è¿›è¡Œå›¾åƒæ•è·å¹¶é€šè¿‡ Wi-Fi è¿æ¥çš„åŠŸèƒ½ã€‚åœ¨ `void setup()` å‡½æ•°ä¸­ï¼Œåˆå§‹åŒ–äº†ä¸²å£ã€æ‘„åƒå¤´å’Œ Wi-Fi è¿æ¥ï¼›å¦‚æœåˆå§‹åŒ–æˆåŠŸï¼Œç¨‹åºä¼šæ‰“å° Wi-Fi åœ°å€ä¾›ç”¨æˆ·è¿æ¥ã€‚åœ¨ `void loop()` å‡½æ•°ä¸­ï¼Œä»£ç æ¯ 10 ç§’æ£€æŸ¥ä¸€æ¬¡ Wi-Fi çŠ¶æ€ï¼Œå¦‚æœæ²¡æœ‰æ‘„åƒå¤´æ“ä½œï¼ŒWi-Fi å°†è¢«è®¾ç½®ä¸ºç¡çœ æ¨¡å¼ä»¥èŠ‚çœèƒ½æºã€‚æ¯æ¬¡è°ƒç”¨ `cameraOperation()` å‡½æ•°éƒ½ä¼šæ›´æ–°æœ€åæ“ä½œçš„æ—¶é—´ï¼Œä»¥ç¡®ä¿åœ¨äº‹ä»¶æœŸé—´ Wi-Fi ä¿æŒè¿æ¥ã€‚

</TabItem>

</Tabs>

:::tip
è¿™äº›ä»£ç ä¸èƒ½ç›´æ¥ä½¿ç”¨ï¼Œæ‚¨éœ€è¦æ·»åŠ å…³äºæ‘„åƒå¤´çš„å¤´æ–‡ä»¶ï¼Œè¯·æŸ¥çœ‹ä¸Šé¢å…³äº XIAO ESP32 S3 çš„ç¤ºä¾‹ã€‚
:::

## æ€»ç»“

### ä¸ºä»€ä¹ˆä½¿ç”¨æ·±åº¦ç¡çœ æ¨¡å¼

åœ¨ä¸å½±å“åŠŸèƒ½çš„å‰æä¸‹æœ€å¤§åŒ–èŠ‚èƒ½ï¼Œä»¥å»¶é•¿è®¾å¤‡çš„ç”µæ± å¯¿å‘½ã€‚
é€‚ç”¨åœºæ™¯ï¼šç”µæ± å¯¿å‘½è‡³å…³é‡è¦çš„åº”ç”¨ï¼Œå¦‚è¿œç¨‹ä¼ æ„Ÿå™¨èŠ‚ç‚¹ã€å¯ç©¿æˆ´è®¾å¤‡å’Œå…¶ä»–ä½åŠŸè€—ç‰©è”ç½‘è®¾å¤‡ã€‚è™½ç„¶å”¤é†’æ—¶é—´ç›¸å¯¹è¾ƒæ…¢ï¼Œä½†è¿™ç§æƒè¡¡æ˜¯å€¼å¾—çš„ã€‚

### ä¸ºä»€ä¹ˆä½¿ç”¨è°ƒåˆ¶è§£è°ƒå™¨ç¡çœ æ¨¡å¼

ä¼˜åŒ–æ— çº¿é€šä¿¡æ¨¡å—çš„åŠŸè€—ï¼ŒåŒæ—¶ä»ä¿æŒç½‘ç»œè¿æ¥ã€‚
é€‚ç”¨åœºæ™¯ï¼šéœ€è¦ä¿æŒç½‘ç»œè¿æ¥ä½†ä¹Ÿéœ€è¦ä½åŠŸè€—çš„åº”ç”¨ï¼Œå¦‚é—´æ­‡å·¥ä½œçš„ç‰©è”ç½‘è®¾å¤‡ã€‚è°ƒåˆ¶è§£è°ƒå™¨ç¡çœ å¯ä»¥æ˜¾è‘—é™ä½æ— çº¿æ¨¡å—çš„åŠŸè€—ï¼ŒåŒæ—¶ä»æä¾›å¿«é€Ÿå”¤é†’å“åº”ã€‚

### æ€»ç»“

è¿™ä¸‰ç§ç¡çœ æ¨¡å¼ä¸ºå¼€å‘è€…æä¾›äº†ä¸åŒçš„åŠŸè€—/æ€§èƒ½æƒè¡¡é€‰é¡¹ï¼Œå¯ä»¥æ ¹æ®åº”ç”¨çš„å…·ä½“è¦æ±‚çµæ´»é€‰æ‹©ã€‚å¯¹äºæœ‰ç”µæ± å¯¿å‘½è¦æ±‚çš„è®¾å¤‡ï¼Œæ·±åº¦ç¡çœ æ¨¡å¼æ˜¯ä¸€ä¸ªå¥½é€‰æ‹©ï¼›è€Œå¯¹äºéœ€è¦ä¿æŒç½‘ç»œè¿æ¥çš„ç‰©è”ç½‘è®¾å¤‡ï¼Œè°ƒåˆ¶è§£è°ƒå™¨ç¡çœ æ¨¡å¼æ˜¯æœ€ä½³é€‰æ‹©ã€‚

## æŠ€æœ¯æ”¯æŒä¸äº§å“è®¨è®º

æ„Ÿè°¢æ‚¨é€‰æ‹©æˆ‘ä»¬çš„äº§å“ï¼æˆ‘ä»¬åœ¨è¿™é‡Œä¸ºæ‚¨æä¾›ä¸åŒçš„æ”¯æŒï¼Œä»¥ç¡®ä¿æ‚¨ä½¿ç”¨æˆ‘ä»¬äº§å“çš„ä½“éªŒå°½å¯èƒ½é¡ºç•…ã€‚æˆ‘ä»¬æä¾›å¤šç§æ²Ÿé€šæ¸ é“ï¼Œä»¥æ»¡è¶³ä¸åŒçš„åå¥½å’Œéœ€æ±‚ã€‚

<div class="button_tech_support_container">
<a href="https://forum.seeedstudio.com/" class="button_forum"></a>
<a href="https://www.seeedstudio.com/contacts" class="button_email"></a>
</div>

<div class="button_tech_support_container">
<a href="https://discord.gg/eWkprNDMU7" class="button_discord"></a>
<a href="https://github.com/Seeed-Studio/wiki-documents/discussions/69" class="button_discussion"></a>
</div>
