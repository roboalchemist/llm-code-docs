---
description: æœ¬æ–‡ä»‹ç»å¦‚ä½•ä½¿ç”¨ XIAO ESP32S3 Sense æ‰©å±•æ¿ä¸Šçš„éº¦å…‹é£ã€‚
title: Sense ç‰ˆæœ¬çš„éº¦å…‹é£ä½¿ç”¨
keywords:
- Template
image: https://files.seeedstudio.com/wiki/seeed_logo/logo_2023.png
slug: /cn/xiao_esp32s3_sense_mic
last_update:
  date: 04/14/2023
  author: Citric
---

# Seeed Studio XIAO ESP32S3 éº¦å…‹é£çš„ä½¿ç”¨

åœ¨æœ¬æ•™ç¨‹ä¸­ï¼Œæˆ‘ä»¬å°†ä¸ºæ‚¨ä»‹ç»å¦‚ä½•ä½¿ç”¨ XIAO ESP32S3 Sense æ‰©å±•æ¿çš„éº¦å…‹é£ã€‚é¦–å…ˆæ˜¯ I2S å¼•è„šçš„åŸºæœ¬ä½¿ç”¨ï¼Œæˆ‘ä»¬å°†é€šè¿‡ä½¿ç”¨ I2S å’Œéº¦å…‹é£è·å–å½“å‰ç¯å¢ƒçš„å“åº¦ï¼Œå¹¶åœ¨ä¸²å£æ³¢å½¢å›¾ä¸­æ˜¾ç¤ºã€‚ç„¶åæˆ‘ä»¬å°†è§£é‡Šå¦‚ä½•å½•åˆ¶å£°éŸ³å¹¶å°†å½•åˆ¶çš„å£°éŸ³ä¿å­˜åˆ° SD å¡ä¸­ã€‚

<div class="table-center">
  <table align="center">
    <tr>
        <th>Seeed Studio XIAO ESP32S3 Sense</th>
    </tr>
    <tr>
        <td><div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/SeeedStudio-XIAO-ESP32S3/img/xiaoesp32s3sense.jpg" style={{width:250, height:'auto'}}/></div></td>
    </tr>
      <tr>
        <td><div class="get_one_now_container" style={{textAlign: 'center'}}>
          <a class="get_one_now_item" href="https://www.seeedstudio.com/XIAO-ESP32S3-Sense-p-5639.html" target="_blank">
              <strong><span><font color={'FFFFFF'} size={"4"}> ç«‹å³è´­ä¹° ğŸ–±ï¸</font></span></strong>
          </a>
      </div></td>
    </tr>
  </table>
</div>

:::caution
æœ¬æ•™ç¨‹çš„æ‰€æœ‰å†…å®¹ä»…é€‚ç”¨äº XIAO ESP32S3 Senseã€‚
:::

## å…¥é—¨æŒ‡å—

åœ¨å¼€å§‹æ•™ç¨‹å†…å®¹ä¹‹å‰ï¼Œæ‚¨å¯èƒ½éœ€è¦æå‰å‡†å¤‡ä»¥ä¸‹ç¡¬ä»¶å’Œè½¯ä»¶ã€‚

### æ‰©å±•æ¿çš„å®‰è£…ï¼ˆé€‚ç”¨äº Senseï¼‰

å®‰è£…æ‰©å±•æ¿éå¸¸ç®€å•ï¼Œæ‚¨åªéœ€è¦å°†æ‰©å±•æ¿ä¸Šçš„è¿æ¥å™¨ä¸ XIAO ESP32S3 ä¸Šçš„ B2B è¿æ¥å™¨å¯¹é½ï¼Œç”¨åŠ›æŒ‰ä¸‹å¹¶å¬åˆ°"å’”å—’"å£°ï¼Œå®‰è£…å°±å®Œæˆäº†ã€‚

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/SeeedStudio-XIAO-ESP32S3/img/61.gif" style={{width:500, height:'auto'}}/></div>

### å‡†å¤‡ microSD å¡

åœ¨è¿›è¡Œä¿å­˜å½•éŸ³çš„é¡¹ç›®æ—¶ï¼Œæ‚¨å¯èƒ½éœ€è¦ä¸€å¼  MicroSD å¡ã€‚

XIAO ESP32S3 Sense æ”¯æŒæœ€å¤§ **32GB** çš„ microSD å¡ï¼Œæ‰€ä»¥å¦‚æœæ‚¨å‡†å¤‡ä¸º XIAO è´­ä¹° microSD å¡ï¼Œè¯·å‚è€ƒæ­¤è§„æ ¼ã€‚åœ¨ä½¿ç”¨ microSD å¡ä¹‹å‰ï¼Œè¯·å°† microSD å¡æ ¼å¼åŒ–ä¸º **FAT32** æ ¼å¼ã€‚

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/SeeedStudio-XIAO-ESP32S3/img/67.png" style={{width:250, height:'auto'}}/></div>

æ ¼å¼åŒ–åï¼Œæ‚¨å¯ä»¥å°† microSD å¡æ’å…¥ microSD å¡æ§½ã€‚è¯·æ³¨æ„æ’å…¥æ–¹å‘ï¼Œæœ‰é‡‘æ‰‹æŒ‡çš„ä¸€é¢åº”æœå†…ã€‚

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/SeeedStudio-XIAO-ESP32S3/img/66.jpg" style={{width:500, height:'auto'}}/></div>

### äº†è§£å¼•è„š

<div class="table-center">
	<table align="center">
    <tr>
			<th>å¼•è„šç¼–å·</th>
			<th>åŠŸèƒ½æè¿°</th>
		</tr>
		<tr>
			<td align="center">GPIO 41</td>
			<td align="center">PDM éº¦å…‹é£ DATA</td>
		</tr>
		<tr>
			<td align="center">GPIO 42</td>
			<td align="center">PDM éº¦å…‹é£ CLK</td>
		</tr>
	</table>
</div>

## å£°éŸ³å“åº¦æ£€æµ‹

å¯¹äºç¬¬ä¸€ä¸ªé¡¹ç›®æ¡ˆä¾‹ï¼Œè®©æˆ‘ä»¬æ£€æµ‹ç¯å¢ƒä¸­çš„å™ªéŸ³ï¼Œå¹¶ä½¿ç”¨ Arduino IDE çš„ä¸²å£æ³¢å½¢å›¾æ˜¾ç¤ºéº¦å…‹é£æ£€æµ‹åˆ°çš„ç¯å¢ƒå“åº¦ã€‚

ä»¥ä¸‹æ˜¯å®Œæ•´çš„ç¤ºä¾‹ç¨‹åºã€‚

:::tip
æ£€æŸ¥å¹¶ç¡®è®¤æ‚¨ä½¿ç”¨çš„ `esp32` ç‰ˆæœ¬ï¼Œä»¥ä¸‹ç¤ºä¾‹é€‚ç”¨äº 2.0.xï¼Œä¸‹é¢çš„ç¤ºä¾‹é€‚ç”¨äº 3.0.x åŠæ›´é«˜ç‰ˆæœ¬
:::

```cpp
#include <I2S.h>

void setup() {
  // Open serial communications and wait for port to open:
  // A baud rate of 115200 is used instead of 9600 for a faster data rate
  // on non-native USB ports
  Serial.begin(115200);
  while (!Serial) {
    ; // wait for serial port to connect. Needed for native USB port only
  }

  // start I2S at 16 kHz with 16-bits per sample
  I2S.setAllPins(-1, 42, 41, -1, -1);
  if (!I2S.begin(PDM_MONO_MODE, 16000, 16)) {
    Serial.println("Failed to initialize I2S!");
    while (1); // do nothing
  }
}

void loop() {
  // read a sample
  int sample = I2S.read();

  if (sample && sample != -1 && sample != 1) {
    Serial.println(sample);
  }
}
```

:::tip
ä¸Šé¢çš„ç¤ºä¾‹ä»…ä¸ `esp32` çš„ 2.0.x å…¼å®¹ï¼Œå¦‚æœæ‚¨ä½¿ç”¨çš„æ˜¯æœ€æ–°ç‰ˆæœ¬ï¼ˆä¾‹å¦‚ 3.0.xï¼‰ï¼Œè¯·ä½¿ç”¨ä¸‹é¢çš„ç¤ºä¾‹
:::

```cpp
#include <ESP_I2S.h>
I2SClass I2S;

void setup() {
  // Open serial communications and wait for port to open:
  // A baud rate of 115200 is used instead of 9600 for a faster data rate
  // on non-native USB ports
  Serial.begin(115200);
  while (!Serial) {
    ; // wait for serial port to connect. Needed for native USB port only
  }

  // setup 42 PDM clock and 41 PDM data pins
  I2S.setPinsPdmRx(42, 41);

  // start I2S at 16 kHz with 16-bits per sample
  if (!I2S.begin(I2S_MODE_PDM_RX, 16000, I2S_DATA_BIT_WIDTH_16BIT, I2S_SLOT_MODE_MONO)) {
    Serial.println("Failed to initialize I2S!");
    while (1); // do nothing
  }
}

void loop() {
  // read a sample
  int sample = I2S.read();

  if (sample && sample != -1 && sample != 1) {
    Serial.println(sample);
  }
}
```

ä¸º XIAO ESP32S3 Sense ä¸Šä¼ æ­¤ç¨‹åºå¹¶æ‰“å¼€ **ä¸²å£ç»˜å›¾å™¨**ï¼Œæ‚¨å°†çœ‹åˆ°å£°éŸ³çš„å“åº¦å˜åŒ–æ›²çº¿ã€‚

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/SeeedStudio-XIAO-ESP32S3/img/83.png" style={{width:600, height:'auto'}}/></div>

### ç¨‹åºæ³¨é‡Š

åœ¨ç¨‹åºå¼€å§‹æ—¶ï¼Œæˆ‘ä»¬éœ€è¦é¦–å…ˆå¯¼å…¥ I2S åº“ä»¥ä¾¿ä½¿ç”¨éº¦å…‹é£å¼•è„šã€‚

```c
#include <I2S.h>
```

åœ¨ I2S å¯¹è±¡ä¸Šè°ƒç”¨ `setAllPins()` å‡½æ•°æ¥é…ç½®ç”¨äº I2S æ¥å£çš„å¼•è„šã€‚è¯¥å‡½æ•°æ¥å—äº”ä¸ªæ•´æ•°å‚æ•°ï¼Œåˆ†åˆ«è¡¨ç¤ºè¿æ¥åˆ° I2S æ¥å£çš„ä½æ—¶é’Ÿã€å­—é€‰æ‹©ã€æ•°æ®è¾“å…¥ã€æ•°æ®è¾“å‡ºå’Œé€šé“é€‰æ‹©çº¿çš„ GPIO å¼•è„šã€‚

```c
I2S.setAllPins(-1, 42, 41, -1, -1);
```

åœ¨è¿™ä¸ªç‰¹å®šçš„ä»£ç ä¸­ï¼Œ`-1` å€¼è¡¨ç¤ºç›¸åº”çš„å¼•è„šæœªä½¿ç”¨ï¼Œè€Œ `42` å’Œ `41` å€¼åˆ†åˆ«è¡¨ç¤ºè¿æ¥åˆ°å­—é€‰æ‹©å’Œæ•°æ®è¾“å…¥çº¿çš„ GPIO å¼•è„šã€‚æ•°æ®è¾“å‡ºå’Œé€šé“é€‰æ‹©çº¿åœ¨æ­¤é…ç½®ä¸­æœªä½¿ç”¨ï¼Œè®¾ç½®ä¸º `-1`ã€‚

```c
if (!I2S.begin(PDM_MONO_MODE, 16000, 16)) {
    Serial.println("Failed to initialize I2S!");
    while (1); // do nothing
}
```

åœ¨ I2S å¯¹è±¡ä¸Šè°ƒç”¨ `begin()` å‡½æ•°ï¼Œä½¿ç”¨æŒ‡å®šå‚æ•°åˆå§‹åŒ– I2S æ¥å£ï¼š`PDM_MONO_MODE`ã€`16000` Hz é‡‡æ ·ç‡å’Œ `16 ä½` åˆ†è¾¨ç‡ã€‚

:::tip
éœ€è¦æ³¨æ„çš„æ˜¯ï¼Œå¯¹äºå½“å‰çš„ ESP32-S3 èŠ¯ç‰‡ï¼Œæˆ‘ä»¬åªèƒ½ä½¿ç”¨ `PDM_MONO_MODE`ï¼Œé‡‡æ ·ä½å®½åªèƒ½æ˜¯ `16bit`ã€‚åªæœ‰é‡‡æ ·ç‡å¯ä»¥ä¿®æ”¹ï¼Œä½†ç»è¿‡æµ‹è¯•ï¼Œ16kHz çš„é‡‡æ ·ç‡ç›¸å¯¹ç¨³å®šã€‚
:::

```c
int sample = I2S.read();

if (sample && sample != -1 && sample != 1) {
    Serial.println(sample);
}
```

åœ¨ I2S å¯¹è±¡ä¸Šè°ƒç”¨ `read()` å‡½æ•°ä» I2S æ¥å£è¯»å–å•ä¸ªéŸ³é¢‘æ ·æœ¬ã€‚if è¯­å¥æ£€æŸ¥ sample å˜é‡çš„å€¼ã€‚å¦‚æœæ ·æœ¬å€¼ä¸æ˜¯ 0ã€-1 æˆ– 1ï¼Œåˆ™è¢«è®¤ä¸ºæ˜¯æœ‰æ•ˆçš„éŸ³é¢‘æ ·æœ¬ï¼Œif å—å†…çš„ä»£ç å°†è¢«æ‰§è¡Œã€‚åœ¨è¿™ç§æƒ…å†µä¸‹ï¼Œä½¿ç”¨ `Serial.println()` å‡½æ•°å°†æ ·æœ¬å€¼æ‰“å°åˆ°ä¸²å£æ§åˆ¶å°ã€‚

## å°†å½•åˆ¶çš„å£°éŸ³ä¿å­˜åˆ° microSD å¡

åœ¨ä¸‹ä¸€ä¸ªé¡¹ç›®ä¸­ï¼Œæˆ‘ä»¬å°†æŒ‡å¯¼æ‚¨å¦‚ä½•ç»“åˆ microSD å¡çš„åŠŸèƒ½ï¼Œå°†å½•åˆ¶çš„å£°éŸ³ä¿å­˜åˆ° microSD å¡ä¸­ã€‚å¯¹äºè¿™ä¸ªé¡¹ç›®ï¼Œè¯·æå‰å‡†å¤‡ microSD å¡å¹¶å°†å…¶æ ¼å¼åŒ–ä¸º **FAT32** æ ¼å¼ã€‚

å¦‚æœè¿™æ˜¯æ‚¨ç¬¬ä¸€æ¬¡åœ¨ XIAO ESP32S3 ä¸Šä½¿ç”¨ microSD å¡ï¼Œæ‚¨å¯ä»¥é˜…è¯»[æ–‡ä»¶ç³»ç»Ÿ Wiki](https://wiki.seeedstudio.com/cn/xiao_esp32s3_sense_filesystem/#prepare-the-microsd-card) å†…å®¹æ¥äº†è§£ microSD å¡çš„å®‰è£…å’Œå‡†å¤‡ã€‚

ä»¥ä¸‹æ˜¯æ­¤é¡¹ç›®çš„ Arduino ç¨‹åºã€‚
:::tip
æ£€æŸ¥å¹¶ç¡®è®¤æ‚¨ä½¿ç”¨çš„ `esp32` ç‰ˆæœ¬ï¼Œä»¥ä¸‹ç¤ºä¾‹é€‚ç”¨äº 2.0.xï¼Œä¸‹é¢çš„ç¤ºä¾‹é€‚ç”¨äº 3.0.x åŠæ›´é«˜ç‰ˆæœ¬
:::

```cpp
/* 
 * WAV Recorder for Seeed XIAO ESP32S3 Sense 
*/

#include <I2S.h>
#include "FS.h"
#include "SD.h"
#include "SPI.h"

// make changes as needed
#define RECORD_TIME   20  // seconds, The maximum value is 240
#define WAV_FILE_NAME "arduino_rec"

// do not change for best
#define SAMPLE_RATE 16000U
#define SAMPLE_BITS 16
#define WAV_HEADER_SIZE 44
#define VOLUME_GAIN 2

void setup() {
  Serial.begin(115200);
  while (!Serial) ;
  I2S.setAllPins(-1, 42, 41, -1, -1);
  if (!I2S.begin(PDM_MONO_MODE, SAMPLE_RATE, SAMPLE_BITS)) {
    Serial.println("Failed to initialize I2S!");
    while (1) ;
  }
  if(!SD.begin(21)){
    Serial.println("Failed to mount SD Card!");
    while (1) ;
  }
  record_wav();
}

void loop() {
  delay(1000);
  Serial.printf(".");
}

void record_wav()
{
  uint32_t sample_size = 0;
  uint32_t record_size = (SAMPLE_RATE * SAMPLE_BITS / 8) * RECORD_TIME;
  uint8_t *rec_buffer = NULL;
  Serial.printf("Ready to start recording ...\n");

  File file = SD.open("/"WAV_FILE_NAME".wav", FILE_WRITE);
  // Write the header to the WAV file
  uint8_t wav_header[WAV_HEADER_SIZE];
  generate_wav_header(wav_header, record_size, SAMPLE_RATE);
  file.write(wav_header, WAV_HEADER_SIZE);

  // PSRAM malloc for recording
  rec_buffer = (uint8_t *)ps_malloc(record_size);
  if (rec_buffer == NULL) {
    Serial.printf("malloc failed!\n");
    while(1) ;
  }
  Serial.printf("Buffer: %d bytes\n", ESP.getPsramSize() - ESP.getFreePsram());

  // Start recording
  esp_i2s::i2s_read(esp_i2s::I2S_NUM_0, rec_buffer, record_size, &sample_size, portMAX_DELAY);
  if (sample_size == 0) {
    Serial.printf("Record Failed!\n");
  } else {
    Serial.printf("Record %d bytes\n", sample_size);
  }

  // Increase volume
  for (uint32_t i = 0; i < sample_size; i += SAMPLE_BITS/8) {
    (*(uint16_t *)(rec_buffer+i)) <<= VOLUME_GAIN;
  }

  // Write data to the WAV file
  Serial.printf("Writing to the file ...\n");
  if (file.write(rec_buffer, record_size) != record_size)
    Serial.printf("Write file Failed!\n");

  free(rec_buffer);
  file.close();
  Serial.printf("The recording is over.\n");
}

void generate_wav_header(uint8_t *wav_header, uint32_t wav_size, uint32_t sample_rate)
{
  // See this for reference: http://soundfile.sapp.org/doc/WaveFormat/
  uint32_t file_size = wav_size + WAV_HEADER_SIZE - 8;
  uint32_t byte_rate = SAMPLE_RATE * SAMPLE_BITS / 8;
  const uint8_t set_wav_header[] = {
    'R', 'I', 'F', 'F', // ChunkID
    file_size, file_size >> 8, file_size >> 16, file_size >> 24, // ChunkSize
    'W', 'A', 'V', 'E', // Format
    'f', 'm', 't', ' ', // Subchunk1ID
    0x10, 0x00, 0x00, 0x00, // Subchunk1Size (16 for PCM)
    0x01, 0x00, // AudioFormat (1 for PCM)
    0x01, 0x00, // NumChannels (1 channel)
    sample_rate, sample_rate >> 8, sample_rate >> 16, sample_rate >> 24, // SampleRate
    byte_rate, byte_rate >> 8, byte_rate >> 16, byte_rate >> 24, // ByteRate
    0x02, 0x00, // BlockAlign
    0x10, 0x00, // BitsPerSample (16 bits)
    'd', 'a', 't', 'a', // Subchunk2ID
    wav_size, wav_size >> 8, wav_size >> 16, wav_size >> 24, // Subchunk2Size
  };
  memcpy(wav_header, set_wav_header, sizeof(set_wav_header));
}
```

:::tip
ä¸Šé¢çš„ç¤ºä¾‹ä»…ä¸ `esp32` çš„ 2.0.x ç‰ˆæœ¬å…¼å®¹ï¼Œå¦‚æœæ‚¨ä½¿ç”¨çš„æ˜¯æœ€æ–°ç‰ˆæœ¬ï¼ˆä¾‹å¦‚ 3.0.xï¼‰ï¼Œè¯·ä½¿ç”¨ä¸‹é¢çš„ç¤ºä¾‹
:::

```cpp
#include "ESP_I2S.h"
#include "FS.h"
#include "SD.h"

void setup() {
  // Create an instance of the I2SClass
  I2SClass i2s;

  // Create variables to store the audio data
  uint8_t *wav_buffer;
  size_t wav_size;

  // Initialize the serial port
  Serial.begin(115200);
  while (!Serial) {
    delay(10);
  }

  Serial.println("Initializing I2S bus...");

  // Set up the pins used for audio input
  i2s.setPinsPdmRx(42, 41);

  // start I2S at 16 kHz with 16-bits per sample
  if (!i2s.begin(I2S_MODE_PDM_RX, 16000, I2S_DATA_BIT_WIDTH_16BIT, I2S_SLOT_MODE_MONO)) {
    Serial.println("Failed to initialize I2S!");
    while (1); // do nothing
  }

  Serial.println("I2S bus initialized.");
  Serial.println("Initializing SD card...");

  // Set up the pins used for SD card access
  if(!SD.begin(21)){
    Serial.println("Failed to mount SD Card!");
    while (1) ;
  }
  Serial.println("SD card initialized.");
  Serial.println("Recording 20 seconds of audio data...");

  // Record 20 seconds of audio data
  wav_buffer = i2s.recordWAV(20, &wav_size);

  // Create a file on the SD card
  File file = SD.open("/arduinor_rec.wav", FILE_WRITE);
  if (!file) {
    Serial.println("Failed to open file for writing!");
    return;
  }

  Serial.println("Writing audio data to file...");

  // Write the audio data to the file
  if (file.write(wav_buffer, wav_size) != wav_size) {
    Serial.println("Failed to write audio data to file!");
    return;
  }

  // Close the file
  file.close();

  Serial.println("Application complete.");
}

void loop() {
  delay(1000);
  Serial.printf(".");
}
```

è¦æ‰§è¡Œæ­¤ç¤ºä¾‹ï¼Œæˆ‘ä»¬éœ€è¦ä½¿ç”¨ ESP-32 èŠ¯ç‰‡çš„ PSRAM åŠŸèƒ½ï¼Œå› æ­¤è¯·åœ¨ä¸Šä¼ ä¹‹å‰å°†å…¶æ‰“å¼€ã€‚

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/SeeedStudio-XIAO-ESP32S3/img/94.png" style={{width:700, height:'auto'}}/></div>

æ­¤ç¨‹åºä»…åœ¨ç”¨æˆ·**æ‰“å¼€ä¸²å£ç›‘è§†å™¨**åæ‰§è¡Œä¸€æ¬¡ï¼Œå½•åˆ¶ 20 ç§’å¹¶å°†å½•åˆ¶æ–‡ä»¶ä¿å­˜åˆ° microSD å¡ä¸­ï¼Œæ–‡ä»¶åä¸º"arduino_rec.wav"ã€‚

å½“ä¸²å£ç›‘è§†å™¨æ¯ 1 ç§’è¾“å‡ºä¸€ä¸ª"."æ—¶ï¼Œç¨‹åºæ‰§è¡Œå®Œæˆï¼Œæ‚¨å¯ä»¥å€ŸåŠ©è¯»å¡å™¨æ’­æ”¾å½•åˆ¶çš„å£°éŸ³æ–‡ä»¶ã€‚

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/SeeedStudio-XIAO-ESP32S3/img/102.png" style={{width:700, height:'auto'}}/></div>

:::tip
è¦æ’­æ”¾ä» XIAO ESP32S3 å½•åˆ¶çš„éŸ³é¢‘ï¼Œæ‚¨å¯èƒ½éœ€è¦ä½¿ç”¨æ”¯æŒ WAV æ ¼å¼çš„éŸ³é¢‘æ’­æ”¾å™¨ã€‚
:::

### ç¨‹åºæ³¨é‡Š

åœ¨è¿™ä¸ªç¨‹åºä¸­ï¼Œæˆ‘ä»¬ä¸ºå½•éŸ³åŠŸèƒ½ç¼–å†™äº†ä¸¤ä¸ªå‡½æ•°ï¼Œä¸€ä¸ªæ˜¯ `record_wav()`ï¼Œå¦ä¸€ä¸ªæ˜¯ `generate_wav_header()`ã€‚

- `record_wav()`ï¼šå½•éŸ³å‡½æ•°ã€‚åœ¨æ­¤å‡½æ•°ä¸­ï¼Œç¨‹åºä½¿ç”¨ I2S æ¥å£ä»éº¦å…‹é£è¯»å–éŸ³é¢‘æ•°æ®ï¼Œå¹¶å°†å…¶ä½œä¸º WAV éŸ³é¢‘æ–‡ä»¶å­˜å‚¨åˆ° SD å¡ä¸­ã€‚

  a. åˆå§‹åŒ–å˜é‡ã€‚ç¨‹åºå®šä¹‰äº†ä¸€ä¸ªç”¨äºå­˜å‚¨å½•åˆ¶æ•°æ®çš„ç¼“å†²åŒº `rec_buffer`ï¼Œå¹¶è®¾ç½®å½•åˆ¶æ—¶é—´ `RECORD_TIME`ã€‚

  b. æ‰“å¼€ WAV æ–‡ä»¶ã€‚ç¨‹åºä½¿ç”¨ `SD.open()` å‡½æ•°æ‰“å¼€ä¸€ä¸ª WAV éŸ³é¢‘æ–‡ä»¶ï¼Œå¹¶å°†å…¶æ–‡ä»¶åå®šä¹‰ä¸º `WAV_FILE_NAME`ã€‚

  c. å†™å…¥ WAV æ–‡ä»¶å¤´ã€‚ç¨‹åºä½¿ç”¨ `generate_wav_header()` å‡½æ•°ç”Ÿæˆ WAV éŸ³é¢‘æ–‡ä»¶çš„å¤´ä¿¡æ¯ï¼Œå¹¶å°†å…¶å†™å…¥æ‰“å¼€çš„ WAV æ–‡ä»¶ä¸­ã€‚

  d. åˆ†é…å†…å­˜å¹¶å¼€å§‹å½•åˆ¶ã€‚ç¨‹åºä½¿ç”¨ `ps_malloc()` å‡½æ•°åœ¨ ESP32S3 çš„ PSRAM ä¸­åˆ†é…ä¸€å—å†…å­˜ç”¨äºå­˜å‚¨å½•åˆ¶æ•°æ®ï¼Œå¹¶ä½¿ç”¨ `esp_i2s::i2s_read()` å‡½æ•°ä»éº¦å…‹é£è¯»å–éŸ³é¢‘æ•°æ®ã€‚è¯»å–çš„æ•°æ®å­˜å‚¨åœ¨ rec_buffer ç¼“å†²åŒºä¸­ã€‚

  e. å¢åŠ éŸ³é‡ã€‚ç¨‹åºä½¿ç”¨ç”± `VOLUME_GAIN` å¸¸é‡å®šä¹‰çš„å¢ç›Šå€¼æ¥å¢åŠ å½•åˆ¶æ•°æ®çš„éŸ³é‡ã€‚

  f. å°†å½•åˆ¶æ•°æ®å†™å…¥ WAV æ–‡ä»¶ã€‚ç¨‹åºä½¿ç”¨ `file.write()` å‡½æ•°å°†å½•åˆ¶æ•°æ®å†™å…¥æ‰“å¼€çš„ WAV æ–‡ä»¶ä¸­ã€‚

  g. é‡Šæ”¾ç¼“å†²åŒºå†…å­˜å¹¶å…³é—­ WAV æ–‡ä»¶ã€‚ç¨‹åºä½¿ç”¨ `free()` å‡½æ•°é‡Šæ”¾ `rec_buffer` ç¼“å†²åŒºçš„å†…å­˜ï¼Œå¹¶ä½¿ç”¨ `file.close()` å‡½æ•°å…³é—­æ‰“å¼€çš„ WAV æ–‡ä»¶ã€‚

- `generate_wav_header(uint8_t *wav_header, uint32_t wav_size, uint32_t sample_rate)`ï¼šç”¨äºç”Ÿæˆ WAV æ–‡ä»¶å¤´ä¿¡æ¯çš„å‡½æ•°ã€‚åœ¨æ­¤å‡½æ•°ä¸­ï¼Œç¨‹åºæ ¹æ® WAV æ–‡ä»¶çš„è§„èŒƒç”ŸæˆåŒ…å« WAV æ–‡ä»¶å¤´ä¿¡æ¯çš„å­—èŠ‚æ•°ç»„ã€‚

  a. å®šä¹‰ WAV æ–‡ä»¶å¤´ä¿¡æ¯çš„å¸¸é‡ã€‚ç¨‹åºå®šä¹‰äº†ä¸€ä¸ªåŒ…å« WAV æ–‡ä»¶å¤´ä¿¡æ¯çš„å­—èŠ‚æ•°ç»„ `set_wav_header`ï¼Œå¹¶å®šä¹‰äº† WAV æ–‡ä»¶è§„èŒƒçš„å¸¸é‡ï¼ŒåŒ…æ‹¬ `NUM_CHANNELS`ã€`BITS_PER_SAMPLE`ã€`WAV_HEADER_SIZE` å’Œ `SUB_CHUNK_SIZE`ã€‚

  b. è®¾ç½® WAV æ–‡ä»¶å¤´ä¿¡æ¯ã€‚ç¨‹åºä½¿ç”¨æ­¥éª¤ a ä¸­å®šä¹‰çš„å¸¸é‡è®¾ç½® WAV æ–‡ä»¶å¤´ä¿¡æ¯ï¼Œå¹¶æ ¹æ® WAV æ–‡ä»¶è§„èŒƒè®¡ç®—ä¸€äº›å­—æ®µçš„å€¼ï¼ŒåŒ…æ‹¬ `AUDIO_FORMAT`ã€`BYTE_RATE`ã€`BLOCK_ALIGN`ã€`SAMPLES_PER_CHANNEL` å’Œ `CHUNK_SIZE`ã€‚è®¡ç®—å‡ºçš„å€¼å­˜å‚¨åœ¨ `set_wav_header` å­—èŠ‚æ•°ç»„ä¸­ã€‚

  c. å¤åˆ¶ WAV æ–‡ä»¶å¤´ä¿¡æ¯ã€‚ç¨‹åºå°†å­˜å‚¨åœ¨ `set_wav_header` ä¸­çš„å¤´ä¿¡æ¯å¤åˆ¶åˆ°å­—èŠ‚æ•°ç»„ `wav_header` ä¸­ã€‚

## æ•…éšœæ’é™¤

### ä¸ºä»€ä¹ˆæ— æ³•æ’­æ”¾å½•åˆ¶çš„éŸ³é¢‘æ–‡ä»¶ï¼Ÿ

å¦‚æœæ‚¨é‡åˆ°æ— æ³•æ’­æ”¾çš„æƒ…å†µï¼Œè¯·æ£€æŸ¥ä¸²å£ç›‘è§†å™¨æ‰“å°çš„è°ƒè¯•ä¿¡æ¯ä¸­æ˜¯å¦æœ‰å…³äºè¯»å†™å¡çš„é”™è¯¯æ¶ˆæ¯ã€‚å¦‚æœæœ‰ï¼Œè¯·æ›´æ¢ microSD å¡æˆ–æ£€æŸ¥å¡ä¸æ‰©å±•æ¿è¿æ¥æ˜¯å¦æ¾åŠ¨æˆ–ä¸ç¨³å®šã€‚å¦‚æœå¡æ²¡æœ‰é—®é¢˜ï¼Œé‚£ä¹ˆéŸ³é¢‘æ–‡ä»¶åº”è¯¥æœ‰å¤§å°ï¼Œå¦‚æœå½•åˆ¶æœ‰é—®é¢˜ï¼Œå¯èƒ½ä¼šæ˜¾ç¤ºå½•åˆ¶çš„éŸ³é¢‘æ–‡ä»¶å¤§å°åªæœ‰ 0KBã€‚

ä¾‹å¦‚ï¼Œåœ¨ä¸‹å›¾ä¸­ï¼Œè¯»å†™å¡å­˜åœ¨é—®é¢˜ã€‚

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/SeeedStudio-XIAO-ESP32S3/img/103.png" style={{width:600, height:'auto'}}/></div>

å¦‚æœå¡æ²¡æœ‰é—®é¢˜ä¸”å½•åˆ¶ç›¸å½“æˆåŠŸã€‚é‚£ä¹ˆæ‚¨éœ€è¦æ£€æŸ¥è½¯ä»¶æ˜¯å¦æ”¯æŒ WAV æ ¼å¼çš„éŸ³é¢‘æ’­æ”¾ã€‚æˆ‘ä»¬å»ºè®®ä½¿ç”¨ä¸“é—¨çš„éŸ³ä¹æ’­æ”¾è½¯ä»¶æ¥æ’­æ”¾éŸ³é¢‘ï¼Œå°½é‡ä¸è¦ä½¿ç”¨è§†é¢‘æ’­æ”¾å™¨æ¥æ’­æ”¾ã€‚ç»è¿‡å®é™…æµ‹è¯•ï¼Œæœ‰è®¸å¤šè§†é¢‘æ’­æ”¾å™¨ï¼ˆè™½ç„¶å®ƒä»¬æ”¯æŒ WAV æ ¼å¼ï¼‰æ— æ³•æ’­æ”¾ã€‚

## æŠ€æœ¯æ”¯æŒä¸äº§å“è®¨è®º

æ„Ÿè°¢æ‚¨é€‰æ‹©æˆ‘ä»¬çš„äº§å“ï¼æˆ‘ä»¬åœ¨è¿™é‡Œä¸ºæ‚¨æä¾›ä¸åŒçš„æ”¯æŒï¼Œä»¥ç¡®ä¿æ‚¨ä½¿ç”¨æˆ‘ä»¬äº§å“çš„ä½“éªŒå°½å¯èƒ½é¡ºç•…ã€‚æˆ‘ä»¬æä¾›å¤šç§æ²Ÿé€šæ¸ é“ï¼Œä»¥æ»¡è¶³ä¸åŒçš„åå¥½å’Œéœ€æ±‚ã€‚

<div class="button_tech_support_container">
<a href="https://forum.seeedstudio.com/" class="button_forum"></a>
<a href="https://www.seeedstudio.com/contacts" class="button_email"></a>
</div>

<div class="button_tech_support_container">
<a href="https://discord.gg/eWkprNDMU7" class="button_discord"></a>
<a href="https://github.com/Seeed-Studio/wiki-documents/discussions/69" class="button_discussion"></a>
</div>
