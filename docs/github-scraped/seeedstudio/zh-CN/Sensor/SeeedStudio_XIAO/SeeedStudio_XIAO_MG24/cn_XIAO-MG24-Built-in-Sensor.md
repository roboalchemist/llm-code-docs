---
title: Seeed Studio XIAO MG24 Sense å†…ç½®ä¼ æ„Ÿå™¨
description: æœ¬æ–‡ä»‹ç»å¦‚ä½•ä½¿ç”¨ XIAO MG24 Sense ä¸Šçš„éº¦å…‹é£ã€‚
image: https://files.seeedstudio.com/wiki/mg24_mic/mg24.jpg
slug: /cn/xiao_mg24_sense_built_in_sensor
keywords:
  - XIAO
  - MG24
last_update:
  date: 11/20/2024 
  author: Jason
sidebar_position: 3
---

# Seeed Studio XIAO MG24 Sense å†…ç½®ä¼ æ„Ÿå™¨ä½¿ç”¨æ–¹æ³•

## XIAO MG24 Sense IMU

### å†…ç½®ä¼ æ„Ÿå™¨æ¦‚è¿°

**6è½´ IMUï¼ˆæƒ¯æ€§æµ‹é‡å•å…ƒï¼‰** ä¼ æ„Ÿå™¨å¦‚ **LSM6DS3TR-C** é›†æˆäº†åŠ é€Ÿåº¦è®¡å’Œé™€èºä»ªï¼Œç”¨äºæµ‹é‡ç‰©ä½“åœ¨ä¸‰ç»´ç©ºé—´ä¸­çš„è¿åŠ¨å’Œæ–¹å‘ã€‚å…·ä½“æ¥è¯´ï¼ŒLSM6DS3TR-C å…·æœ‰ä»¥ä¸‹ç‰¹æ€§ï¼š

**åŠ é€Ÿåº¦è®¡åŠŸèƒ½ï¼š**

- æµ‹é‡ç‰©ä½“æ²¿ Xã€Y å’Œ Z è½´çš„åŠ é€Ÿåº¦ã€‚å®ƒèƒ½å¤Ÿæ„ŸçŸ¥ç‰©ä½“è¿åŠ¨ï¼ˆä¾‹å¦‚ï¼Œé™æ­¢ã€åŠ é€Ÿã€å‡é€Ÿï¼‰å’Œå€¾æ–œå˜åŒ–ï¼ˆä¾‹å¦‚ï¼Œç‰©ä½“çš„è§’åº¦ï¼‰ã€‚
- å¯ç”¨äºæ£€æµ‹æ­¥æ€ã€ä½ç½®å˜åŒ–ã€æŒ¯åŠ¨ç­‰ã€‚

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/xyz1.5.jpg" style={{width:320, height:'auto'}}/></div>

**é™€èºä»ªåŠŸèƒ½ï¼š**

- æµ‹é‡ç‰©ä½“å›´ç»• Xã€Y å’Œ Z è½´çš„è§’é€Ÿåº¦ï¼Œå³ç‰©ä½“çš„æ—‹è½¬ã€‚
- å¯ç”¨äºæ£€æµ‹æ—‹è½¬ã€æ—‹è½¬é€Ÿç‡å’Œæ–¹å‘å˜åŒ–ã€‚

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/xyz2.0.jpg" style={{width:320, height:'auto'}}/></div>

- **Xè½´è§’åº¦ï¼ˆRollï¼‰** æ˜¯å›´ç»•Xè½´æ—‹è½¬æ–¹å‘çš„è§’åº¦ã€‚
- **Yè½´è§’åº¦ï¼ˆPitchï¼‰** æ˜¯å›´ç»•Yè½´æ—‹è½¬æ–¹å‘çš„è§’åº¦ã€‚
- **Zè½´è§’åº¦ï¼ˆYawï¼‰** æ˜¯å›´ç»•Zè½´æ—‹è½¬æ–¹å‘çš„è§’åº¦ã€‚

### è½¯ä»¶å‡†å¤‡

<div class="github_container" style={{textAlign: 'center'}}>
    <a class="github_item" href="https://github.com/Seeed-Studio/Seeed_Arduino_LSM6DS3" target="_blank" rel="noopener noreferrer">
    <strong><span><font color={'FFFFFF'} size={"4"}> ä¸‹è½½åº“æ–‡ä»¶</font></span></strong> <svg aria-hidden="true" focusable="false" role="img" className="mr-2" viewBox="-3 10 9 1" width={16} height={16} fill="currentColor" style={{textAlign: 'center', display: 'inline-block', userSelect: 'none', verticalAlign: 'text-bottom', overflow: 'visible'}}><path d="M8 0c4.42 0 8 3.58 8 8a8.013 8.013 0 0 1-5.45 7.59c-.4.08-.55-.17-.55-.38 0-.27.01-1.13.01-2.2 0-.75-.25-1.23-.54-1.48 1.78-.2 3.65-.88 3.65-3.95 0-.88-.31-1.59-.82-2.15.08-.2.36-1.02-.08-2.12 0 0-.67-.22-2.2.82-.64-.18-1.32-.27-2-.27-.68 0-1.36.09-2 .27-1.53-1.03-2.2-.82-2.2-.82-.44 1.1-.16 1.92-.08 2.12-.51.56-.82 1.28-.82 2.15 0 3.06 1.86 3.75 3.64 3.95-.23.2-.44.55-.51 1.07-.46.21-1.61.55-2.33-.66-.15-.24-.6-.83-1.23-.82-.67.01-.27.38.01.53.34.19.73.9.82 1.13.16.45.68 1.31 2.69.94 0 .67.01 1.3.01 1.49 0 .21-.15.45-.55.38A7.995 7.995 0 0 1 0 8c0-4.42 3.58-8 8-8Z" /></svg>
    </a>
</div>

ç‚¹å‡» github ä¸‹è½½é“¾æ¥è·å–å…­è½´ä¼ æ„Ÿå™¨é©±åŠ¨åº“ã€‚

### ä»£ç å®ç°

```cpp

#include <LSM6DS3.h>
#include <Wire.h>

//Create a instance of class LSM6DS3
LSM6DS3 myIMU(I2C_MODE, 0x6A);    //I2C device address 0x6A
float aX, aY, aZ, gX, gY, gZ;
const float accelerationThreshold = 2.5; // threshold of significant in G's
const int numSamples = 119;
int samplesRead = numSamples;

void setup() {
  // put your setup code here, to run once:
  Serial.begin(9600);
  while (!Serial);

  pinMode(PD5,OUTPUT);
  digitalWrite(PD5,HIGH);
  //Call .begin() to configure the IMUs
  if (myIMU.begin() != 0) {
    Serial.println("Device error");
  } else {
    Serial.println("aX,aY,aZ,gX,gY,gZ");
  }
}

void loop() {
  // wait for significant motion
  while (samplesRead == numSamples) {
    // read the acceleration data
    aX = myIMU.readFloatAccelX();
    aY = myIMU.readFloatAccelY();
    aZ = myIMU.readFloatAccelZ();

    // sum up the absolutes
    float aSum = fabs(aX) + fabs(aY) + fabs(aZ);

    // check if it's above the threshold
    if (aSum >= accelerationThreshold) {
      // reset the sample read count
      samplesRead = 0;
      break;
    }
  }

  // check if the all the required samples have been read since
  // the last time the significant motion was detected
  while (samplesRead < numSamples) {
    // check if both new acceleration and gyroscope data is
    // available
    // read the acceleration and gyroscope data

    samplesRead++;

    // print the data in CSV format
    Serial.print(myIMU.readFloatAccelX(), 3);
    Serial.print(',');
    Serial.print(myIMU.readFloatAccelY(), 3);
    Serial.print(',');
    Serial.print(myIMU.readFloatAccelZ(), 3);
    Serial.print(',');
    Serial.print(myIMU.readFloatGyroX(), 3);
    Serial.print(',');
    Serial.print(myIMU.readFloatGyroY(), 3);
    Serial.print(',');
    Serial.print(myIMU.readFloatGyroZ(), 3);
    Serial.println();

    if (samplesRead == numSamples) {
      // add an empty line if it's the last sample
      Serial.println();
    }
  }
}
```

:::tip

ç”±äº LSM6DS3 åº“çš„æ›´æ–°ï¼Œå¦‚æœæ‚¨ä¹‹å‰å·²ç»æ·»åŠ äº†æ­¤åº“ï¼Œæ‚¨éœ€è¦é‡æ–°ä¸‹è½½ 2.0.4 æˆ–æ›´é«˜ç‰ˆæœ¬å¹¶å°† ZIP æ–‡ä»¶æ·»åŠ åˆ° Arduinoã€‚

:::

### åŠŸèƒ½æ¦‚è¿°

- **åŒ…å«åº“**

  ```cpp
    #include <LSM6DS3.h> 
    #include <Wire.h>
  ```

  - åŒ…å«ç”¨äºä¸LSM6DS3ä¼ æ„Ÿå™¨é€šä¿¡çš„åº“ã€‚
  - åŒ…å«ç”¨äºI2Cé€šä¿¡çš„åº“ã€‚

- **åˆ›å»ºä¼ æ„Ÿå™¨å®ä¾‹**
  - `LSM6DS3 myIMU(I2C_MODE, 0x6A)` ä¸ºIMUä¼ æ„Ÿå™¨åˆ›å»ºLSM6DS3ç±»çš„å®ä¾‹ï¼ŒæŒ‡å®šI2Cé€šä¿¡æ¨¡å¼å’Œè®¾å¤‡åœ°å€0x6Aã€‚

- **å˜é‡å’Œå¸¸é‡**
  - `float aX, aY, aZ, gX, gY, gZ`ï¼šç”¨äºå­˜å‚¨åŠ é€Ÿåº¦è®¡å’Œé™€èºä»ªæ•°æ®çš„å˜é‡ã€‚
  - `const float accelerationThreshold = 2.5`ï¼šç”¨äºæ£€æµ‹æ˜¾è‘—è¿åŠ¨çš„é˜ˆå€¼ï¼Œå•ä½ä¸ºGã€‚
  - `const int numSamples = 119`ï¼šæ£€æµ‹åˆ°æ˜¾è‘—è¿åŠ¨åè¦æ”¶é›†çš„æ ·æœ¬æ•°é‡ã€‚
  - `int samplesRead = numSamples`ï¼šå°†æ ·æœ¬è®¡æ•°å™¨åˆå§‹åŒ–ä¸ºæ€»æ ·æœ¬æ•°ï¼Œè¡¨ç¤ºå°šæœªæ”¶é›†ä»»ä½•æ•°æ®ã€‚

- **åŸºæœ¬è®¾ç½®**

  ```cpp
    pinMode(PD5,OUTPUT);
    digitalWrite(PD5,HIGH);
  ```

  - æ‰“å¼€é™€èºä»ªä½¿èƒ½å¼•è„šã€‚

- **æ•°æ®å¤„ç†**

    ```cpp
    aX = myIMU.readFloatAccelX();:
    aY = myIMU.readFloatAccelY();:
    aZ = myIMU.readFloatAccelZ();:
    float aSum = fabs(aX) + fabs(aY) + fabs(aZ);
    ```

  - è¯»å–æ²¿Xè½´çš„åŠ é€Ÿåº¦ã€‚
  - è¯»å–æ²¿Yè½´çš„åŠ é€Ÿåº¦ã€‚
  - è¯»å–æ²¿Zè½´çš„åŠ é€Ÿåº¦ã€‚
  - è®¡ç®—åŠ é€Ÿåº¦æ•°æ®ç»å¯¹å€¼çš„æ€»å’Œï¼Œ`fabs()`è¿”å›ç»å¯¹å€¼ã€‚

  ```cpp
    // check if it's above the threshold
    if (aSum >= accelerationThreshold) {
      // reset the sample read count
      samplesRead = 0;
      break;
    }
  ```

  - å¦‚æœç»å¯¹åŠ é€Ÿåº¦å€¼çš„æ€»å’Œå¤§äºæˆ–ç­‰äºè®¾å®šçš„é˜ˆå€¼ï¼Œåˆ™å°†é‡‡æ ·è®¡æ•° samplesRead é‡ç½®ä¸º 0 å¹¶é€€å‡ºå¾ªç¯ã€‚

- **æ£€æŸ¥æ•°æ®**

  ```cpp
  while (samplesRead < numSamples) {
    samplesRead++;
    .
    .
    .
    .
    .
    if (samplesRead == numSamples) {
      // add an empty line if it's the last sample
      Serial.println();
    }
  }
  ```

  - è¿›å…¥å¦ä¸€ä¸ªå¾ªç¯å¹¶æ£€æŸ¥æ˜¯å¦å·²è¯»å–æ‰€éœ€æ•°é‡çš„æ ·æœ¬ã€‚
  - å¢åŠ  samplesRead çš„è®¡æ•°ã€‚
  - å¦‚æœæ‰€æœ‰æ ·æœ¬éƒ½å·²è¯»å–ï¼Œæ‰“å°ä¸€ä¸ªç©ºè¡Œæ¥åˆ†éš”æ•°æ®è¾“å‡ºã€‚

### ç»“æœå›¾è¡¨

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/six_resutl.png" style={{width:700, height:'auto'}}/></div>

### æ›´å¤šå†…å®¹

å¦‚æœæ‚¨æƒ³è¦æ›´å¤šç¤ºä¾‹ä»£ç ï¼Œè¯·ç‚¹å‡»ï¼š**"File" -> Example -> Seeed Arduino LSM6DS3"**

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/33.png" style={{width:500, height:'auto'}}/></div>

## IMU é«˜çº§æ¼”ç¤º

### ç¡¬ä»¶å‡†å¤‡

<div class="table-center">
 <table align="center">
  <tr>
   <th>Seeeduino-XIAO-Expansion-Board</th>
   <th>Seeed Studio XIAO MG24 Sense</th>
  </tr>
  <tr>
   <td><div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/Seeeduino-XIAO-Expansion-Board/Update_pic/zheng1.jpg" style={{width:250, height:'auto'}}/></div></td>
   <td><div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/XIAO_MG24/Getting_Start/shop.jpg" style={{width:250, height:'auto'}}/></div></td>
  </tr>
  <tr>
   <td><div class="get_one_now_container" style={{textAlign: 'center'}}>
    <a class="get_one_now_item" href="https://www.seeedstudio.com/Seeeduino-XIAO-Expansion-board-p-4746.html" target="_blank">
    <strong><span><font color={'FFFFFF'} size={"4"}> ç«‹å³è·å– ğŸ–±ï¸</font></span></strong>
    </a>
   </div></td>
   <td><div class="get_one_now_container" style={{textAlign: 'center'}}>
    <a class="get_one_now_item" href="https://www.seeedstudio.com/Seeed-XIAO-MG24-Sense-p-6248.html" target="_blank">
    <strong><span><font color={'FFFFFF'} size={"4"}> ç«‹å³è·å– ğŸ–±ï¸</font></span></strong>
    </a>
   </div></td>
  </tr>
 </table>
</div>

### è½¯ä»¶å‡†å¤‡

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/arduino_mouse.jpg" style={{width:500, height:'auto'}}/></div>

:::tip
æˆ‘ä»¬éœ€è¦åœ¨å·¥å…·æ ä¸­é€‰æ‹©ç›¸åº”çš„å †æ ˆæ¥çƒ§å½•ç¨‹åºã€‚
:::

<details>

<summary>ç¨‹åºä»£ç </summary>

```cpp
#include <LSM6DS3.h>
#include "Wire.h"

#define DEVICE_NAME "XIAO MG24 Mouse"

#define IMU_ACC_X_THRESHOLD 10
#define IMU_ACC_Y_THRESHOLD 10

// Mouse button events
#define LMB_PRESSED 1

// HID report data
struct mouse_data {
  int8_t delta_x;
  int8_t delta_y;
  uint8_t buttons;
};
static mouse_data report;

// HID report data buffer
static uint8_t report_array[] = { 0x00, 0x00, 0x00 };

static uint8_t connection_handle = SL_BT_INVALID_CONNECTION_HANDLE;
static uint32_t bonding_handle = SL_BT_INVALID_BONDING_HANDLE;
static uint16_t hid_report;

// Device information service
const uint8_t manufacturer[] = "Silicon Labs";
const uint8_t model_no[] = "1";
const uint8_t serial_no[] = "1";
const uint8_t hw_rev[] = "1";
const uint8_t fw_rev[] = "1";
const uint8_t sw_rev[] = "1";

static bd_addr ble_address = { 0x00u, 0x00u, 0x00u, 0x00u, 0x00u, 0x00u };
static bool button_press = false;
static bool button_press_prev = false;
static int32_t acc_x = 0, acc_y = 0;
static sl_status_t sc = SL_STATUS_OK;

static void ble_initialize_gatt_db();
static void ble_start_advertising();
static void mouse_button_callback();

// Optionally connect a button for left click
#define MOUSE_BUTTON D1

LSM6DS3 myIMU(I2C_MODE, 0x6A);

void setup()
{
  // Initialize report data
  memset(&report, 0, sizeof(report));

  // Enable the IMU power
  pinMode(PD5, OUTPUT);
  digitalWrite(PD5, HIGH);
  delay(300);

  pinMode(MOUSE_BUTTON, INPUT_PULLUP);
  attachInterrupt(MOUSE_BUTTON, mouse_button_callback, CHANGE);

  Serial.begin(115200);

  Serial.println("XIAO MG24 BLE mouse");

  myIMU.begin();
  Serial.println("---");
  Serial.println("IMU initialized");
}

void loop()
{
  // Update 'left mouse button' bit
  if (button_press) {
    report.buttons |= LMB_PRESSED;
    if (!button_press_prev) {
      button_press_prev = true;
      Serial.println("Button pressed");
    }
  } else {
    button_press_prev = false;
    report.buttons &= ~LMB_PRESSED;
  }

  // Change x and y for correct orientation of the boards
  acc_y = (int32_t)(myIMU.readFloatAccelX() * 10.0f);
  acc_x = (int32_t)(myIMU.readFloatAccelY() * 10.0f * -1.0f);

  // In case the acceleration value would surpass the threshold value
  // in positive or negative direction assign the threshold value
  if (acc_x > IMU_ACC_X_THRESHOLD) {
    report.delta_x = IMU_ACC_X_THRESHOLD;
  } else if (acc_x < (-1 * IMU_ACC_X_THRESHOLD)) {
    report.delta_x = (-1 * IMU_ACC_X_THRESHOLD);
  } else {
    report.delta_x = acc_x;
  }

  if (acc_y > IMU_ACC_Y_THRESHOLD) {
    report.delta_y = IMU_ACC_Y_THRESHOLD;
  } else if (acc_y < (-1 * IMU_ACC_Y_THRESHOLD)) {
    report.delta_y = (-1 * IMU_ACC_Y_THRESHOLD);
  } else {
    report.delta_y = acc_y;
  }

  memcpy(report_array, &report, sizeof(report));
  if (connection_handle != SL_BT_INVALID_CONNECTION_HANDLE && bonding_handle != SL_BT_INVALID_BONDING_HANDLE) {
    // Indicate report data change with GATT notification
    sc = sl_bt_gatt_server_notify_all(hid_report, sizeof(report_array), report_array);
    if (sc != SL_STATUS_OK) {
      Serial.print("sl_bt_gatt_server_notify_all() returned with error code 0x");
      Serial.println(sc, HEX);
    } else {
      Serial.print("cursor [delta-X: ");
      Serial.print(report.delta_x, DEC);
      Serial.print(" delta-Y: ");
      Serial.print(report.delta_y, DEC);
      Serial.print(" ] LMB: ");
      Serial.println(report.buttons, HEX);
    }
  }
}

/******************************************************************************
 * Mouse button callback
 *****************************************************************************/
void mouse_button_callback()
{
  if (digitalRead(MOUSE_BUTTON) == LOW) {
    button_press = true;
  } else {
    button_press = false;
  }
}

/******************************************************************************
 * Bluetooth stack event handler
 * Called when an event happens on BLE the stack
 *
 * @param[in] evt Event coming from the Bluetooth stack
 *****************************************************************************/
void sl_bt_on_event(sl_bt_msg_t* evt)
{
  sl_status_t sc = SL_STATUS_OK;
  uint8_t ble_address_type;

  switch (SL_BT_MSG_ID(evt->header)) {
    // -------------------------------
    // This event indicates the device has started and the radio is ready
    case sl_bt_evt_system_boot_id:
    {
      // Get BLE address and address type
      sc = sl_bt_system_get_identity_address(&ble_address, &ble_address_type);
      app_assert_status(sc);

      // Print welcome message
      Serial.begin(115200);
      Serial.println();
      Serial.println("BLE stack booted");

      // Initialize the application specific GATT DB
      ble_initialize_gatt_db();

      // HID input devices requires mandatory secure level and bonding
      sc = sl_bt_sm_configure(0, sl_bt_sm_io_capability_noinputnooutput);
      app_assert_status(sc);

      // Allow bonding
      sc = sl_bt_sm_set_bondable_mode(1);
      app_assert_status(sc);

      ble_start_advertising();
    }
    break;

    // -------------------------------
    // This event indicates that a BLE connection has been opened
    case sl_bt_evt_connection_opened_id:
    {
      // Store the connection handle which will be needed for sending indications
      connection_handle = evt->data.evt_connection_opened.connection;
      bonding_handle = evt->data.evt_connection_opened.bonding;
      Serial.print("Connection opened - handle 0x");
      Serial.println(connection_handle, HEX);

      if (bonding_handle == SL_BT_INVALID_BONDING_HANDLE) {
        Serial.println("Connection not bonded yet");
      } else {
        Serial.println("Connection bonded");
      }

      Serial.println("Increase security");
      sc = sl_bt_sm_increase_security(evt->data.evt_connection_opened.connection);
      app_assert_status(sc);
    }
    break;

    // -------------------------------
    // This event indicates that bonding was successful
    case sl_bt_evt_sm_bonded_id:
    {
      Serial.print("Bonded - handle: 0x");
      Serial.print(evt->data.evt_sm_bonded.connection, HEX);
      bonding_handle = evt->data.evt_sm_bonded.bonding;
      connection_handle = evt->data.evt_sm_bonded.connection;

      Serial.print(" - security mode: 0x");
      Serial.println(evt->data.evt_sm_bonded.security_mode, HEX);
    }
    break;

    // -------------------------------
    // This event indicates that a BLE connection has closed
    case sl_bt_evt_connection_closed_id:
    {
      Serial.print("Connection closed - handle: 0x");
      Serial.print(connection_handle, HEX);
      Serial.print(" reason: 0x");
      Serial.println(evt->data.evt_connection_closed.reason, HEX);

      connection_handle = SL_BT_INVALID_CONNECTION_HANDLE;
      bonding_handle = SL_BT_INVALID_BONDING_HANDLE;

      sc = sl_bt_sm_delete_bondings();
      Serial.println("Deleted bondings");
      app_assert_status(sc);

      ble_start_advertising();
    }
    break;

    // -------------------------------
    // This event indicates that the connection parameters have changed
    case sl_bt_evt_connection_parameters_id:
    {
      Serial.print("Set connection parameters, security_mode: ");
      Serial.println(evt->data.evt_connection_parameters.security_mode, HEX);
    }
    break;

    // -------------------------------
    // This event indicates that bonding has failed
    case sl_bt_evt_sm_bonding_failed_id:
    {
      Serial.print("Bonding failed, reason: 0x");
      Serial.println(evt->data.evt_sm_bonding_failed.reason, HEX);
      Serial.println("Delete bondings.");

      sc = sl_bt_sm_delete_bondings();
      app_assert_status(sc);

      Serial.println("Bondings deleted");
      Serial.print("Close connection - handle: 0x");
      Serial.println(evt->data.evt_sm_bonding_failed.connection, HEX);
    }
    break;

    // -------------------------------
    // Default event handler
    default:
      break;
  }
}

/******************************************************************************
 * Starts BLE advertisement
 * Initializes advertising if it's called for the first time
 *****************************************************************************/
static void ble_start_advertising()
{
  static uint8_t advertising_set_handle = 0xff;
  static bool init = true;
  sl_status_t sc;

  if (init) {
    // Create an advertising set
    sc = sl_bt_advertiser_create_set(&advertising_set_handle);
    app_assert_status(sc);

    // Set advertising interval to 100ms
    sc = sl_bt_advertiser_set_timing(
      advertising_set_handle,
      160,  // Minimum advertisement interval (milliseconds * 1.6)
      160,  // Maximum advertisement interval (milliseconds * 1.6)
      0,    // Advertisement duration
      0);   // Maximum number of advertisement events
    app_assert_status(sc);

    init = false;
  }

  // Generate data for advertising
  sc = sl_bt_legacy_advertiser_generate_data(advertising_set_handle, sl_bt_advertiser_general_discoverable);
  app_assert_status(sc);

  // Start advertising and enable connections
  sc = sl_bt_legacy_advertiser_start(advertising_set_handle, sl_bt_advertiser_connectable_scannable);
  app_assert_status(sc);

  Serial.print("Started advertising as '");
  Serial.print(DEVICE_NAME);
  Serial.print("' address: ");
  // Print address in format 'FF:FF:FF:FF:FF:FF'
  for (uint8_t i = (sizeof(bd_addr) - 1); i > 0; i--) {
    Serial.print(ble_address.addr[i], HEX);
    Serial.print(":");
  }
  Serial.println(ble_address.addr[0], HEX);
}

/******************************************************************************
 * Initializes the GATT database
 * Creates a new GATT session and adds certain services and characteristics
 *****************************************************************************/
static void ble_initialize_gatt_db()
{
  sl_status_t sc;
  uint16_t gattdb_session_id;
  uint16_t service;
  uint16_t characteristic;
  uint16_t descriptor;

  // Create a new GATT database
  sc = sl_bt_gattdb_new_session(&gattdb_session_id);
  app_assert_status(sc);

  // Generic access service
  uint8_t generic_access_service_uuid[] = { 0x00, 0x18 };
  sc = sl_bt_gattdb_add_service(gattdb_session_id,
                                sl_bt_gattdb_primary_service,
                                SL_BT_GATTDB_ADVERTISED_SERVICE,
                                2,
                                generic_access_service_uuid,
                                &service);
  app_assert_status(sc);

  // Device name characteristic
  sl_bt_uuid_16_t device_name_uuid = { .data = { 0x00, 0x2A } };
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              (SL_BT_GATTDB_CHARACTERISTIC_READ | SL_BT_GATTDB_CHARACTERISTIC_WRITE),
                                              0,
                                              0,
                                              device_name_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              strlen(DEVICE_NAME),
                                              strlen(DEVICE_NAME),
                                              (uint8_t *)DEVICE_NAME,
                                              &characteristic);
  app_assert_status(sc);

  // Appearance characteristic
  sl_bt_uuid_16_t appearence_uuid = { .data = { 0x01, 0x2A } };
  const uint8_t appearance_value[] = { 0xC2, 0x03 };
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              SL_BT_GATTDB_CHARACTERISTIC_READ,
                                              0,
                                              0,
                                              appearence_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              2,
                                              sizeof(appearance_value),
                                              appearance_value,
                                              &characteristic);
  app_assert_status(sc);

  // Generic access service start
  sc = sl_bt_gattdb_start_service(gattdb_session_id, service);
  app_assert_status(sc);

  // Battery service
  const uint8_t battery_service_uuid[] = { 0x0F, 0x18 };
  sc = sl_bt_gattdb_add_service(gattdb_session_id,
                                sl_bt_gattdb_primary_service,
                                0,
                                sizeof(battery_service_uuid),
                                battery_service_uuid,
                                &service);
  app_assert_status(sc);

  // Battery level characteristic
  const sl_bt_uuid_16_t battery_level_uuid = { .data = { 0x19, 0x2A } };
  const uint8_t battery_level_init_value = 100;
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              SL_BT_GATTDB_CHARACTERISTIC_READ,
                                              0,
                                              0,
                                              battery_level_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              sizeof(battery_level_init_value),
                                              sizeof(battery_level_init_value),
                                              &battery_level_init_value,
                                              &characteristic);
  app_assert_status(sc);

  // Characteristic presentation format descriptor
  const sl_bt_uuid_16_t chara_presentation_format_descriptor_uuid = { .data = { 0x04, 0x29 } };
  const uint8_t chara_presentation_format_value[] = { 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00 };
  sc = sl_bt_gattdb_add_uuid16_descriptor(gattdb_session_id,
                                          characteristic,
                                          SL_BT_GATTDB_DESCRIPTOR_READ,
                                          0,
                                          chara_presentation_format_descriptor_uuid,
                                          sl_bt_gattdb_fixed_length_value,
                                          sizeof(chara_presentation_format_value),
                                          sizeof(chara_presentation_format_value),
                                          chara_presentation_format_value,
                                          &descriptor);
  app_assert_status(sc);

  // Client characteristic configuration descriptor
  const sl_bt_uuid_16_t client_configuration_descriptor_uuid = { .data = { 0x02, 0x29 } };
  const uint8_t client_configuration_value[] = { 0x00, 0x00 };
  sc = sl_bt_gattdb_add_uuid16_descriptor(gattdb_session_id,
                                          characteristic,
                                          (SL_BT_GATTDB_DESCRIPTOR_READ | SL_BT_GATTDB_DESCRIPTOR_WRITE),
                                          0,
                                          client_configuration_descriptor_uuid,
                                          sl_bt_gattdb_fixed_length_value,
                                          sizeof(client_configuration_value),
                                          sizeof(client_configuration_value),
                                          client_configuration_value,
                                          &descriptor);
  app_assert_status(sc);

  // Battery service start
  sc = sl_bt_gattdb_start_service(gattdb_session_id, service);
  app_assert_status(sc);

  // Device information service
  const uint8_t device_info_service_uuid[] = { 0x0A, 0x18 };
  sc = sl_bt_gattdb_add_service(gattdb_session_id,
                                sl_bt_gattdb_primary_service,
                                0,
                                sizeof(device_info_service_uuid),
                                device_info_service_uuid,
                                &service);
  app_assert_status(sc);

  // Manufacturer name string characteristic
  const sl_bt_uuid_16_t manufacturer_uuid = { .data = { 0x29, 0x2A } };
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              SL_BT_GATTDB_CHARACTERISTIC_READ,
                                              0,
                                              0,
                                              manufacturer_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              (sizeof(manufacturer) - 1),
                                              (sizeof(manufacturer) - 1),
                                              manufacturer,
                                              &characteristic);
  app_assert_status(sc);

  // Model number string characteristic
  const sl_bt_uuid_16_t model_no_uuid = { .data = { 0x24, 0x2A } };
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              SL_BT_GATTDB_CHARACTERISTIC_READ,
                                              0,
                                              0,
                                              model_no_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              (sizeof(model_no) - 1),
                                              (sizeof(model_no) - 1),
                                              model_no,
                                              &characteristic);
  app_assert_status(sc);

  // Serial number string characteristic
  const sl_bt_uuid_16_t serial_no_uuid = { .data = { 0x25, 0x2A } };
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              SL_BT_GATTDB_CHARACTERISTIC_READ,
                                              0,
                                              0,
                                              serial_no_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              (sizeof(serial_no) - 1),
                                              (sizeof(serial_no) - 1),
                                              serial_no,
                                              &characteristic);
  app_assert_status(sc);

  // Hardware revision string characteristic
  const sl_bt_uuid_16_t hw_rev_uuid = { .data = { 0x27, 0x2A } };
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              SL_BT_GATTDB_CHARACTERISTIC_READ,
                                              0,
                                              0,
                                              hw_rev_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              (sizeof(hw_rev) - 1),
                                              (sizeof(hw_rev) - 1),
                                              hw_rev,
                                              &characteristic);
  app_assert_status(sc);

  // Firmware revision string characteristic
  const sl_bt_uuid_16_t fw_rev_uuid = { .data = { 0x26, 0x2A } };
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              SL_BT_GATTDB_CHARACTERISTIC_READ,
                                              0,
                                              0,
                                              fw_rev_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              (sizeof(fw_rev) - 1),
                                              (sizeof(fw_rev) - 1),
                                              fw_rev,
                                              &characteristic);
  app_assert_status(sc);

  // Software revision string characteristic
  const sl_bt_uuid_16_t sw_rev_uuid = { .data = { 0x28, 0x2A } };
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              SL_BT_GATTDB_CHARACTERISTIC_READ,
                                              0,
                                              0,
                                              sw_rev_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              (sizeof(sw_rev) - 1),
                                              (sizeof(sw_rev) - 1),
                                              sw_rev,
                                              &characteristic);
  app_assert_status(sc);

  // System ID characteristic
  const sl_bt_uuid_16_t sys_id_uuid = { .data = { 0x23, 0x2A } };
  const uint8_t sys_id_initial_value[] = { 0x12, 0x34, 0x56, 0xFF, 0xFE, 0x9A, 0xBC, 0xDE };
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              SL_BT_GATTDB_CHARACTERISTIC_READ,
                                              0,
                                              0,
                                              sys_id_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              sizeof(sys_id_initial_value),
                                              sizeof(sys_id_initial_value),
                                              sys_id_initial_value,
                                              &characteristic);
  app_assert_status(sc);

  // PnP ID characteristic
  const sl_bt_uuid_16_t pnp_id_uuid = { .data = { 0x50, 0x2A } };
  const uint8_t pnp_id_initial_value[] = { 0x02, 0x10, 0xC4, 0x00, 0x01, 0x00, 0x01 };
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              SL_BT_GATTDB_CHARACTERISTIC_READ,
                                              0,
                                              0,
                                              pnp_id_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              sizeof(pnp_id_initial_value),
                                              sizeof(pnp_id_initial_value),
                                              pnp_id_initial_value,
                                              &characteristic);
  app_assert_status(sc);

  // Device information service start
  sc = sl_bt_gattdb_start_service(gattdb_session_id, service);
  app_assert_status(sc);

  // HID service
  uint8_t hid_service_uuid[] = { 0x12, 0x18 };
  sc = sl_bt_gattdb_add_service(gattdb_session_id,
                                sl_bt_gattdb_primary_service,
                                SL_BT_GATTDB_ADVERTISED_SERVICE,
                                2,
                                hid_service_uuid,
                                &service);
  app_assert_status(sc);

  // Protocol mode characteristic
  sl_bt_uuid_16_t hid_protocol_mode_uuid = { .data = { 0x4E, 0x2A } };
  const uint8_t hid_protocol_mode_init_value[] = { 1 };
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              (SL_BT_GATTDB_CHARACTERISTIC_READ | SL_BT_GATTDB_CHARACTERISTIC_WRITE_NO_RESPONSE),
                                              0,
                                              0,
                                              hid_protocol_mode_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              sizeof(hid_protocol_mode_init_value),
                                              sizeof(hid_protocol_mode_init_value),
                                              hid_protocol_mode_init_value,
                                              &characteristic);
  app_assert_status(sc);

  // HID report characteristic
  const sl_bt_uuid_16_t hid_report_uuid = { .data = { 0x4D, 0x2A } };
  const uint8_t hid_report_init_value[] = { 0x00, 0x00, 0x00 };
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              (SL_BT_GATTDB_CHARACTERISTIC_READ | SL_BT_GATTDB_CHARACTERISTIC_WRITE | SL_BT_GATTDB_CHARACTERISTIC_NOTIFY),
                                              0,
                                              0,
                                              hid_report_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              sizeof(hid_report_init_value),
                                              sizeof(hid_report_init_value),
                                              hid_report_init_value,
                                              &characteristic);
  app_assert_status(sc);
  hid_report = characteristic;

  // HID report reference descriptor
  const sl_bt_uuid_16_t hid_report_reference_desc_uuid = { .data = { 0x08, 0x29 } };
  const uint8_t hid_report_reference_desc_init_val[] = { 0x00, 0x01 };
  sc = sl_bt_gattdb_add_uuid16_descriptor(gattdb_session_id,
                                          characteristic,
                                          SL_BT_GATTDB_DESCRIPTOR_READ,
                                          SL_BT_GATTDB_ENCRYPTED_READ,
                                          hid_report_reference_desc_uuid,
                                          sl_bt_gattdb_fixed_length_value,
                                          sizeof(hid_report_reference_desc_init_val),
                                          sizeof(hid_report_reference_desc_init_val),
                                          hid_report_reference_desc_init_val,
                                          &descriptor);
  app_assert_status(sc);

  // HID report map characteristic
  const sl_bt_uuid_16_t hid_report_map_uuid = { .data = { 0x4B, 0x2A } };
  const uint8_t hid_report_map_init_value[] = { 0x05, 0x01, // Usage page (Generic Desktop)
                                                0x09, 0x02, // Usage (Mouse)
                                                0xA1, 0x01, // Collection (Application)
                                                0x09, 0x01, //   UsageId (Pointer)
                                                0xA1, 0x00, //   Collection (Physical)
                                                0x09, 0x30, //     UsageId (x)
                                                0x09, 0x31, //     UsageId (y)
                                                0x15, 0x80, //     LogicalMinimum(-128)
                                                0x25, 0x7F, //     LogicalMaximum(127)
                                                0x95, 0x02, //     ReportCount(2)
                                                0x75, 0x08, //     ReportSize(8)
                                                0x81, 0x06, //     Input(Data, Variable, Relative, NoWrap, Linear, PreferredState, NoNullPosition, BitField)
                                                0x05, 0x09, //     UsagePage(Button)
                                                0x19, 0x01, //     UsageIdMin(Button 1)
                                                0x29, 0x03, //     UsageIdMax(Button 3)
                                                0x15, 0x00, //     LogicalMinimum(0)
                                                0x25, 0x01, //     LogicalMaximum(1)
                                                0x95, 0x03, //     ReportCount(3)
                                                0x75, 0x01, //     ReportSize(1)
                                                0x81, 0x02, //     Input(Data, Variable, Absolute, NoWrap, Linear, PreferredState, NoNullPosition, BitField)
                                                0x95, 0x01, //     ReportCount(1)
                                                0x75, 0x05, //     ReportSize(5)
                                                0x81, 0x03, //     Input(Constant, Variable, Absolute, NoWrap, Linear, PreferredState, NoNullPosition, BitField)
                                                0xC0,       //   EndCollection()
                                                0xC0 };     // EndCollection()
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              SL_BT_GATTDB_CHARACTERISTIC_READ,
                                              SL_BT_GATTDB_ENCRYPTED_READ,
                                              0,
                                              hid_report_map_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              sizeof(hid_report_map_init_value),
                                              sizeof(hid_report_map_init_value),
                                              hid_report_map_init_value,
                                              &characteristic);
  app_assert_status(sc);

  // HID external report reference descriptor
  const sl_bt_uuid_16_t hid_external_report_reference_descriptor_uuid = { .data = { 0x07, 0x29 } };
  const uint8_t hid_external_report_reference_value[] = { 0x00, 0x00 };
  sc = sl_bt_gattdb_add_uuid16_descriptor(gattdb_session_id,
                                          characteristic,
                                          SL_BT_GATTDB_DESCRIPTOR_READ,
                                          0,
                                          hid_external_report_reference_descriptor_uuid,
                                          sl_bt_gattdb_fixed_length_value,
                                          sizeof(hid_external_report_reference_value),
                                          sizeof(hid_external_report_reference_value),
                                          hid_external_report_reference_value,
                                          &descriptor);
  app_assert_status(sc);

  // HID information characteristic
  const sl_bt_uuid_16_t hid_info_uuid = { .data = { 0x4A, 0x2A } };
  const uint8_t hid_info_init_value[] = { 0x01, 0x11, 0x00, 0x02 };
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              SL_BT_GATTDB_CHARACTERISTIC_READ,
                                              0,
                                              0,
                                              hid_info_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              sizeof(hid_info_init_value),
                                              sizeof(hid_info_init_value),
                                              hid_info_init_value,
                                              &characteristic);
  app_assert_status(sc);

  // HID control point characteristic
  const sl_bt_uuid_16_t hid_control_point_uuid = { .data = { 0x4C, 0x2A } };
  const uint8_t hid_control_point_init_value[] = { 0x00 };
  sc = sl_bt_gattdb_add_uuid16_characteristic(gattdb_session_id,
                                              service,
                                              SL_BT_GATTDB_CHARACTERISTIC_WRITE_NO_RESPONSE,
                                              0,
                                              0,
                                              hid_control_point_uuid,
                                              sl_bt_gattdb_fixed_length_value,
                                              sizeof(hid_control_point_init_value),
                                              sizeof(hid_control_point_init_value),
                                              hid_control_point_init_value,
                                              &characteristic);
  app_assert_status(sc);

  // HID service start
  sc = sl_bt_gattdb_start_service(gattdb_session_id, service);
  app_assert_status(sc);

  // Commit the GATT DB changes
  sc = sl_bt_gattdb_commit(gattdb_session_id);
  app_assert_status(sc);
}

#ifndef BLE_STACK_SILABS
  #error "This example is only compatible with the Silicon Labs BLE stack. Please select 'BLE (Silabs)' in 'Tools > Protocol stack'."
#endif
```

</details>

### ç»“æœå›¾è¡¨

å½“æˆ‘ä»¬æŒ‰ä¸‹æ‰©å±•æ¿ä¸Šçš„æŒ‰é’®æ—¶ï¼Œå¯ä»¥è§‚å¯Ÿåˆ°é¼ æ ‡äº‹ä»¶è¢«è§¦å‘äº†ï¼

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/hid_mouse.gif" style={{width:500, height:'auto'}}/></div>

## XIAO MG24 Sense éº¦å…‹é£ï¼ˆSeeed Studio æ¼”ç¤ºï¼‰

### å†…ç½®ä¼ æ„Ÿå™¨æ¦‚è¿°

**éº¦å…‹é£ä¼ æ„Ÿå™¨**å¦‚ **MSM381ACT001** æ˜¯ä¸€æ¬¾ MEMSï¼ˆå¾®æœºç”µç³»ç»Ÿï¼‰éº¦å…‹é£ï¼Œè®¾è®¡ç”¨äºä»¥**é«˜çµæ•åº¦å’Œä½å™ªå£°**æ•è·éŸ³é¢‘ä¿¡å·ã€‚å…·ä½“æ¥è¯´ï¼ŒMSM381ACT001 å…·æœ‰ä»¥ä¸‹ç‰¹æ€§ï¼š

**éº¦å…‹é£åŠŸèƒ½ï¼š**

- æ•è·å£°æ³¢å¹¶å°†å…¶è½¬æ¢ä¸ºç”µä¿¡å·ï¼Œèƒ½å¤Ÿåœ¨å„ç§ç¯å¢ƒä¸­æ£€æµ‹éŸ³é¢‘è¾“å…¥ã€‚
- å…·æœ‰å®½é¢‘å“èŒƒå›´ï¼Œé€šå¸¸ä¸º 20 Hz åˆ° 20 kHzï¼Œé€‚ç”¨äºå„ç§éŸ³é¢‘åº”ç”¨ï¼ŒåŒ…æ‹¬è¯­éŸ³è¯†åˆ«å’ŒéŸ³ä¹æ’­æ”¾ã€‚

**ä¸»è¦ç‰¹æ€§**

- é«˜çµæ•åº¦ï¼šèƒ½å¤Ÿæ£€æµ‹å¾®å¼±å£°éŸ³ï¼Œéå¸¸é€‚åˆéœ€è¦ç²¾ç¡®éŸ³é¢‘æ•è·çš„åº”ç”¨ã€‚
- ä½å™ªå£°ï¼šè®¾è®¡æä¾›é«˜ä¿¡å™ªæ¯”ï¼ˆSNRï¼‰ï¼Œå³ä½¿åœ¨å˜ˆæ‚ç¯å¢ƒä¸­ä¹Ÿèƒ½ç¡®ä¿æ¸…æ™°çš„éŸ³é¢‘è¾“å‡ºã€‚
- ç´§å‡‘å°ºå¯¸ï¼šMEMS æŠ€æœ¯å…è®¸å°å‹åŒ–è®¾è®¡ï¼Œä¾¿äºé›†æˆåˆ°æ™ºèƒ½æ‰‹æœºå’Œå¯ç©¿æˆ´è®¾å¤‡ç­‰ä¾¿æºå¼è®¾å¤‡ä¸­ã€‚
- æ•°å­—è¾“å‡ºï¼šæä¾›æ•°å­—ä¿¡å·è¾“å‡ºé€‰é¡¹ï¼ˆä¾‹å¦‚ I2Sï¼‰ï¼Œç®€åŒ–ä¸æ•°å­—ä¿¡å·å¤„ç†å™¨ï¼ˆDSPï¼‰å’Œå¾®æ§åˆ¶å™¨çš„æ¥å£ã€‚

### è½¯ä»¶å‡†å¤‡

<div class="github_container" style={{textAlign: 'center'}}>
    <a class="github_item" href="https://github.com/Seeed-Studio/Seeed_Arduino_Mic" target="_blank" rel="noopener noreferrer">
    <strong><span><font color={'FFFFFF'} size={"4"}> ä¸‹è½½åº“æ–‡ä»¶</font></span></strong> <svg aria-hidden="true" focusable="false" role="img" className="mr-2" viewBox="-3 10 9 1" width={16} height={16} fill="currentColor" style={{textAlign: 'center', display: 'inline-block', userSelect: 'none', verticalAlign: 'text-bottom', overflow: 'visible'}}><path d="M8 0c4.42 0 8 3.58 8 8a8.013 8.013 0 0 1-5.45 7.59c-.4.08-.55-.17-.55-.38 0-.27.01-1.13.01-2.2 0-.75-.25-1.23-.54-1.48 1.78-.2 3.65-.88 3.65-3.95 0-.88-.31-1.59-.82-2.15.08-.2.36-1.02-.08-2.12 0 0-.67-.22-2.2.82-.64-.18-1.32-.27-2-.27-.68 0-1.36.09-2 .27-1.53-1.03-2.2-.82-2.2-.82-.44 1.1-.16 1.92-.08 2.12-.51.56-.82 1.28-.82 2.15 0 3.06 1.86 3.75 3.64 3.95-.23.2-.44.55-.51 1.07-.46.21-1.61.55-2.33-.66-.15-.24-.6-.83-1.23-.82-.67.01-.27.38.01.53.34.19.73.9.82 1.13.16.45.68 1.31 2.69.94 0 .67.01 1.3.01 1.49 0 .21-.15.45-.55.38A7.995 7.995 0 0 1 0 8c0-4.42 3.58-8 8-8Z" /></svg>
    </a>
</div>

ç‚¹å‡» github ä¸‹è½½é“¾æ¥æ¥é©±åŠ¨éº¦å…‹é£ä¼ æ„Ÿå™¨ã€‚

:::tip
ç›®å‰æˆ‘ä»¬éœ€è¦æ‰‹åŠ¨ä¿®æ”¹æ›¿æ¢æ–‡ä»¶ï¼Œåç»­ç›´æ¥ä¸‹è½½åº“å³å¯ä½¿ç”¨ï¼Œè¯·ç­‰å¾…æˆ‘ä»¬çš„ wiki æ›´æ–°ã€‚
:::

- **[æ›¿æ¢æ–‡ä»¶]** [gsdk.a](https://files.seeedstudio.com/wiki/mg24_mic/gsdk_v2.a)

**æ›´æ”¹æ–‡ä»¶è·¯å¾„**

- **/Users/yourname/Library/Arduino15/packages/SiliconLabs/hardware/silabs/2.2.0/variants/xiao_mg24/ble_silabs/**

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/file.png" style={{width:350, height:'auto'}}/></div>

### ä»£ç å®ç°

```cpp
#include <mic.h>
#if defined(WIO_TERMINAL)
#include "processing/filters.h"
#endif

// Settings
#if defined(WIO_TERMINAL)
#define DEBUG 1                 // Enable pin pulse during ISR  
#define SAMPLES 16000*3
#elif defined(ARDUINO_ARCH_NRF52840)
#define DEBUG 1                 // Enable pin pulse during ISR  
#define SAMPLES 800
#elif defined(ARDUINO_SILABS)
#define DEBUG 1                 // Enable pin pulse during ISR  
#define SAMPLES 800
#endif

mic_config_t mic_config{
  .channel_cnt = 1,
  .sampling_rate = 16000,
  .buf_size = 1600,
#if defined(WIO_TERMINAL)
  .debug_pin = 1                // Toggles each DAC ISR (if DEBUG is set to 1)
#elif defined(ARDUINO_ARCH_NRF52840)
  .debug_pin = LED_BUILTIN                // Toggles each DAC ISR (if DEBUG is set to 1)
#elif defined(ARDUINO_SILABS)
  .debug_pin = LED_BUILTIN                // Toggles each DAC ISR (if DEBUG is set to 1)  
#endif
};

#if defined(WIO_TERMINAL)
DMA_ADC_Class Mic(&mic_config);
#elif defined(ARDUINO_ARCH_NRF52840)
NRF52840_ADC_Class Mic(&mic_config);
#elif defined(ARDUINO_SILABS)
MG24_ADC_Class Mic(&mic_config);
#endif

int16_t recording_buf[SAMPLES];
volatile uint8_t recording = 0;
volatile static bool record_ready = false;

#if defined(WIO_TERMINAL)
FilterBuHp filter;
#endif

void setup() {

  Serial.begin(115200);
  while (!Serial) {delay(10);}
  
#if defined(WIO_TERMINAL)  
  pinMode(WIO_KEY_A, INPUT_PULLUP);
#endif

  Mic.set_callback(audio_rec_callback);

  if (!Mic.begin()) {
    Serial.println("Mic initialization failed");
    while (1);
  }

  Serial.println("Mic initialization done.");

}

void loop() { 

#if defined(WIO_TERMINAL)  
if (digitalRead(WIO_KEY_A) == LOW && !recording) {

    Serial.println("Starting sampling");
    recording = 1;
    record_ready = false;  
}
#endif

#if defined(WIO_TERMINAL)  
  if (!recording && record_ready)
#elif defined(ARDUINO_ARCH_NRF52840) || defined(ARDUINO_SILABS)
  if (record_ready)
#endif  
  {
  Serial.println("Finished sampling");
  
  for (int i = 0; i < SAMPLES; i++) {
    
  //int16_t sample = filter.step(recording_buf[i]);
  int16_t sample = recording_buf[i];
  Serial.println(sample);
  }
  
  record_ready = false; 
  }
}

static void audio_rec_callback(uint16_t *buf, uint32_t buf_len) {
  
  static uint32_t idx = 0;
  // Copy samples from DMA buffer to inference buffer
#if defined(WIO_TERMINAL)
  if (recording) 
#endif
  {
    for (uint32_t i = 0; i < buf_len; i++) {
  
      // Convert 12-bit unsigned ADC value to 16-bit PCM (signed) audio value
#if defined(WIO_TERMINAL)
      recording_buf[idx++] = filter.step((int16_t)(buf[i] - 1024) * 16);
      //recording_buf[idx++] = (int16_t)(buf[i] - 1024) * 16;  
#elif defined(ARDUINO_ARCH_NRF52840) || defined(ARDUINO_SILABS)
      recording_buf[idx++] = buf[i];
#endif

      if (idx >= SAMPLES){ 
      idx = 0;
      recording = 0;
      record_ready = true;
      break;
     } 
    }
  }

}
```

### Function Overview

**Microphone Configuration**

  ```cpp
  mic_config_t mic_config{
  .channel_cnt = 1,
  .sampling_rate = 16000,
  .buf_size = 1600,
  #if defined(WIO_TERMINAL)
  .debug_pin = 1
  #elif defined(ARDUINO_ARCH_NRF52840)
  .debug_pin = LED_BUILTIN
  #elif defined(ARDUINO_SILABS)
  .debug_pin = LED_BUILTIN
  #endif
};
  ```

- mic_config_t: å®šä¹‰éº¦å…‹é£é…ç½®ç»“æ„ä½“ã€‚
- channel_cnt: è®¾ç½®ä¸º 1 è¡¨ç¤ºå•å£°é“ã€‚
- sampling_rate: è®¾ç½®ä¸º 16000 Hz ä½œä¸ºé‡‡æ ·é¢‘ç‡ã€‚
- buf_size: è®¾ç½®ä¸º 1600 ä½œä¸ºç¼“å†²åŒºå¤§å°ã€‚
- debug_pin: æ ¹æ®å¹³å°è®¾ç½®è°ƒè¯•å¼•è„šï¼Œç”¨äºè°ƒè¯•æœŸé—´çš„ä¿¡å·æŒ‡ç¤ºã€‚

**éº¦å…‹é£å®ä¾‹åŒ–**

  ```cpp
  #if defined(WIO_TERMINAL)
  DMA_ADC_Class Mic(&mic_config);
  #elif defined(ARDUINO_ARCH_NRF52840)
  NRF52840_ADC_Class Mic(&mic_config);
  #elif defined(ARDUINO_SILABS)
  MG24_ADC_Class Mic(&mic_config);
  #endif
  ```

- æ¡ä»¶ç¼–è¯‘ï¼šä¸ºä¸åŒå¹³å°åˆ›å»ºé€‚å½“çš„éº¦å…‹é£ç±»å®ä¾‹ï¼Œä½¿ç”¨ä¹‹å‰å®šä¹‰çš„é…ç½®ã€‚

**å½•éŸ³ç¼“å†²åŒºå’Œæ ‡å¿—**

  ```cpp
  int16_t recording_buf[SAMPLES];
  volatile uint8_t recording = 0;
  volatile static bool record_ready = false;
  ```

- recording_buf: å®šä¹‰ä¸€ä¸ª SAMPLES æ•°ç»„æ¥å­˜å‚¨å½•éŸ³æ ·æœ¬ã€‚
- recording: ä¸€ä¸ª volatile å˜é‡ï¼Œæ ‡è®°å½“å‰æ˜¯å¦æ­£åœ¨å½•éŸ³ï¼Œä»¥é˜²æ­¢ç¼–è¯‘å™¨ä¼˜åŒ–ã€‚
- record_ready: ä¸€ä¸ª volatile é™æ€å˜é‡ï¼ŒæŒ‡ç¤ºå½•éŸ³æ˜¯å¦å®Œæˆå¹¶å‡†å¤‡å¥½è¿›è¡Œè¿›ä¸€æ­¥å¤„ç†ã€‚

**æ»¤æ³¢å™¨ç¤ºä¾‹ï¼ˆé€‚ç”¨äº WIO Terminalï¼‰**

  ```cpp
  #if defined(WIO_TERMINAL)
  FilterBuHp filter;
  #endif
  ```

- å¦‚æœåœ¨ WIO Terminal ä¸Šï¼Œåˆ›å»ºä¸€ä¸ªé«˜é€šæ»¤æ³¢å™¨å®ä¾‹ç”¨äºæ»¤æ³¢å¤„ç†ã€‚

**setup**

  ```cpp
  void setup() {
  Serial.begin(115200);
  while (!Serial) {delay(10);}
  
#if defined(WIO_TERMINAL)  
  pinMode(WIO_KEY_A, INPUT_PULLUP);
#endif

  Mic.set_callback(audio_rec_callback);

  if (!Mic.begin()) {
    Serial.println("Mic initialization failed");
    while (1);
  }

  Serial.println("Mic initialization done.");
}
  ```

- åˆå§‹åŒ–ä¸²å£ï¼šä»¥115200æ³¢ç‰¹ç‡å¯åŠ¨ä¸²è¡Œé€šä¿¡å¹¶ç­‰å¾…ä¸²å£å‡†å¤‡å°±ç»ªã€‚

- è®¾ç½®å¼•è„šæ¨¡å¼ï¼šåœ¨WIO Terminalä¸Šï¼Œå°†æŒ‰é”®å¼•è„šè®¾ç½®ä¸ºè¾“å…¥ä¸Šæ‹‰æ¨¡å¼ã€‚
- è®¾ç½®å›è°ƒå‡½æ•°ï¼šè°ƒç”¨Mic.set_callback(audio_rec_callback)æ¥æŒ‡å®šå½•åˆ¶éŸ³é¢‘æ—¶çš„å›è°ƒå‡½æ•°ã€‚
- åˆå§‹åŒ–éº¦å…‹é£ï¼šè°ƒç”¨Mic.begin()ï¼Œå¦‚æœåˆå§‹åŒ–å¤±è´¥ï¼Œæ‰“å°é”™è¯¯æ¶ˆæ¯å¹¶è¿›å…¥æ­»å¾ªç¯ã€‚

**loop**

  ```cpp
  void loop() { 
#if defined(WIO_TERMINAL)  
if (digitalRead(WIO_KEY_A) == LOW && !recording) {
    Serial.println("Starting sampling");
    recording = 1;
    record_ready = false;  
}
#endif

#if defined(WIO_TERMINAL)  
  if (!recording && record_ready)
#elif defined(ARDUINO_ARCH_NRF52840) || defined(ARDUINO_SILABS)
  if (record_ready)
#endif  
  {
    Serial.println("Finished sampling");
    
    for (int i = 0; i < SAMPLES; i++) {
      int16_t sample = recording_buf[i];
      Serial.println(sample);
    }
    
    record_ready = false; 
  }
}
  ```

- æ£€æµ‹æŒ‰é”®ï¼šåœ¨ WIO Terminal ä¸Šï¼Œå½“æ£€æµ‹åˆ°æŒ‰é”®è¢«æŒ‰ä¸‹ä¸”æœªåœ¨å½•éŸ³æ—¶ï¼Œå¼€å§‹å½•éŸ³ã€‚
- å®Œæˆé‡‡æ ·ï¼šå¦‚æœæœªåœ¨å½•éŸ³ä¸” record_ready è®¾ç½®ä¸º trueï¼Œåˆ™æ‰“å°"Finished sampling"ã€‚
- éå†å½•éŸ³ç¼“å†²åŒºå¹¶æ‰“å°æ¯ä¸ªé‡‡æ ·å€¼ã€‚

**éŸ³é¢‘å½•éŸ³å›è°ƒå‡½æ•°**

  ```cpp
  static void audio_rec_callback(uint16_t *buf, uint32_t buf_len) {
  static uint32_t idx = 0;
  #if defined(WIO_TERMINAL)
  if (recording) 
  #endif
  {
    for (uint32_t i = 0; i < buf_len; i++) {
      #if defined(WIO_TERMINAL)
      recording_buf[idx++] = filter.step((int16_t)(buf[i] - 1024) * 16);
      #elif defined(ARDUINO_ARCH_NRF52840) || defined(ARDUINO_SILABS)
      recording_buf[idx++] = buf[i];
      #endif

      if (idx >= SAMPLES){ 
        idx = 0;
        recording = 0;
        record_ready = true;
        break;
      } 
    }
  }
}
  ```

- å›è°ƒå‡½æ•°ï¼šåœ¨éŸ³é¢‘å½•åˆ¶æœŸé—´è°ƒç”¨ï¼Œè´Ÿè´£å°†æ ·æœ¬ä» DMA ç¼“å†²åŒºå¤åˆ¶åˆ°å½•åˆ¶ç¼“å†²åŒºã€‚
- æ¡ä»¶ç¼–è¯‘ï¼šå¦‚æœåœ¨ WIO Terminal ä¸Šï¼Œä½¿ç”¨æ»¤æ³¢å™¨å¤„ç†è¾“å…¥ã€‚
- å°† 12 ä½æ— ç¬¦å· ADC å€¼è½¬æ¢ä¸º 16 ä½ PCMï¼ˆæœ‰ç¬¦å·ï¼‰éŸ³é¢‘å€¼ã€‚
- æ ·æœ¬å¡«å……ï¼šå°†æ ·æœ¬å¤åˆ¶åˆ° recording_buf ä¸­å¹¶æ›´æ–°ç´¢å¼• idxã€‚
- å®Œæˆå½•åˆ¶ï¼šå¦‚æœå¡«å……çš„æ ·æœ¬æ•°é‡è¾¾åˆ° SAMPLESï¼Œé‡ç½®ç´¢å¼•ï¼Œæ ‡è®°å½•åˆ¶ç»“æŸå¹¶å°† record_ready è®¾ç½®ä¸º trueã€‚

### ç»“æœå›¾è¡¨

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/mic_result1.png" style={{width:680, height:'auto'}}/></div>

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/mic_result.png" style={{width:680, height:'auto'}}/></div>

è¿™æ˜¯è¯†åˆ«åˆ°çš„å£°éŸ³çš„æ³¢å½¢ï¼Œå½“ä½ å¹æ°”æ—¶ï¼Œå¯ä»¥æ¸…æ¥šåœ°çœ‹åˆ°æ³¢å½¢æŒ¯è¡å¹…åº¦å˜å¤§ã€‚

### æ›´å¤šå†…å®¹

å¦‚æœä½ æƒ³è¦æ›´å¤šç¤ºä¾‹ä»£ç ï¼Œè¯·ç‚¹å‡»ï¼š-> **"Example -> Seeed Arduino Mic"**

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/34.png" style={{width:500, height:'auto'}}/></div>
<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/35.png" style={{width:500, height:'auto'}}/></div>

## XIAO MG24 Sense éº¦å…‹é£ï¼ˆSilicon Labs æ¼”ç¤ºï¼‰

:::tip
æˆ‘ä»¬éœ€è¦ä¸‹è½½æœ€æ–°çš„æ¿è½½åŒ…ï¼ˆ2.3.0ï¼‰æ‰èƒ½åœ¨ç¤ºä¾‹ä¸­æ‰¾åˆ°æœ€æ–°çš„å®˜æ–¹ä»£ç 
:::

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/mg24download.jpg" style={{width:400, height:'auto'}}/></div>

### è½¯ä»¶å‡†å¤‡

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/mg24mic.jpg" style={{width:500, height:'auto'}}/></div>

### ä»£ç å®ç°

```cpp
/*
   Analog microphone volume example

   The example showcases the usage of analog MEMS microphones and dims the
   on-board LED based on the microphone's input volume.
   This example is compatible with all Silicon Labs Arduino boards, however
   it requires an analog microphone on-board or connected to the specified pin.

   Author: Ãron Gyapjas (Silicon Labs)
 */

#include <SilabsMicrophoneAnalog.h>

// This configuration is for the MSM381ACT001 microphone on the Seeed Studio XIAO MG24
// Change it according to your hardware
#define MIC_DATA_PIN  PC9
#define MIC_PWR_PIN   PC8
#define NUM_SAMPLES   128
#define MIC_VALUE_MIN 735
#define MIC_VALUE_MAX 900

// Buffers for storing samples
uint32_t mic_buffer[NUM_SAMPLES];
uint32_t mic_buffer_local[NUM_SAMPLES];

volatile bool data_ready_flag = false;
MicrophoneAnalog micAnalog(MIC_DATA_PIN, MIC_PWR_PIN);
void mic_samples_ready_cb();
void calculate_and_display_voice_level();

void setup()
{
  Serial.begin(115200);
  pinMode(LED_BUILTIN, OUTPUT);

  micAnalog.begin(mic_buffer, NUM_SAMPLES);
  Serial.println("Microphone initialized...");

  micAnalog.startSampling(mic_samples_ready_cb);
  Serial.println("Sampling started...");
}

void loop()
{
  if (data_ready_flag) {
    data_ready_flag = false;
    calculate_and_display_voice_level();
  }
}

// Called when the requested amount of samples are available from the microphone
void mic_samples_ready_cb()
{
  // Copy data to the local buffer in order to prevent it from overwriting
  memcpy(mic_buffer_local, mic_buffer, NUM_SAMPLES * sizeof(uint32_t));
  data_ready_flag = true;
}

void calculate_and_display_voice_level() {
  // Rolling average for smoothing the voice level
  static uint32_t rolling_average = 0u;

  // Stop sampling in order to prevent overwriting the current data
  micAnalog.stopSampling();

  // Get the average of the sampled values
  uint32_t voice_level = (uint32_t)micAnalog.getAverage(mic_buffer_local, NUM_SAMPLES);
  // Adjust the voice level relative to minimum/maximum of the microphone's output
  voice_level = constrain(voice_level, MIC_VALUE_MIN, MIC_VALUE_MAX);
  // Calculate the rolling average
  rolling_average = (voice_level + rolling_average) / 2;

  // Map the current average level to brightness
  int brightness = map(rolling_average, MIC_VALUE_MIN, MIC_VALUE_MAX, 0, 255);
  if (LED_BUILTIN_ACTIVE == LOW) {
    analogWrite(LED_BUILTIN, 255 - brightness);
  } else {
    analogWrite(LED_BUILTIN, brightness);
  }
  // Print the average voice level (you can use the Serial Plotter to view this value on a graph)
  Serial.println(rolling_average);

  // Restart sampling
  micAnalog.startSampling(mic_samples_ready_cb);
}

```

### Function Overview

***Header file introduction***

```cpp
#include <SilabsMicrophoneAnalog.h>
```

- åŒ…å« `SilabsMicrophoneAnalog.h` å¤´æ–‡ä»¶ï¼Œè¯¥æ–‡ä»¶åŒ…å«ä½¿ç”¨æ¨¡æ‹Ÿéº¦å…‹é£æ‰€éœ€çš„åº“å‡½æ•°å’Œå®šä¹‰ã€‚

***ç¡¬ä»¶é…ç½®***

```cpp
#define MIC_DATA_PIN  PC9
#define MIC_PWR_PIN   PC8
#define NUM_SAMPLES   128
#define MIC_VALUE_MIN 735
#define MIC_VALUE_MAX 900
```

- `MIC_DATA_PIN`: å®šä¹‰éº¦å…‹é£æ•°æ®å¼•è„šä¸º `PC9`ã€‚

- `MIC_PWR_PIN`: å®šä¹‰éº¦å…‹é£ç”µæºå¼•è„šä¸º `PC8`ã€‚

- `NUM_SAMPLES`: å®šä¹‰æ¯æ¬¡é‡‡æ ·çš„æ ·æœ¬æ•°ä¸º 128ã€‚

- `MIC_VALUE_MIN` å’Œ `MIC_VALUE_MAX`: å®šä¹‰éº¦å…‹é£è¾“å‡ºçš„æœ€å°å€¼å’Œæœ€å¤§å€¼èŒƒå›´ã€‚

***ç¼“å†²åŒºå®šä¹‰***

```cpp
uint32_t mic_buffer[NUM_SAMPLES];
uint32_t mic_buffer_local[NUM_SAMPLES];
```

- `mic_buffer`ï¼šç”¨äºå­˜å‚¨ä»éº¦å…‹é£æ”¶é›†çš„åŸå§‹é‡‡æ ·æ•°æ®ã€‚

- `mic_buffer_local`ï¼šç”¨äºä¸´æ—¶å­˜å‚¨é‡‡æ ·æ•°æ®ä»¥é˜²æ­¢è¦†ç›–ã€‚

***æ ‡å¿—å’Œå¯¹è±¡å®šä¹‰***

```cpp
volatile bool data_ready_flag = false;
MicrophoneAnalog micAnalog(MIC_DATA_PIN, MIC_PWR_PIN);
```

- `data_ready_flag`ï¼šç”¨äºæŒ‡ç¤ºæ–°é‡‡æ ·æ•°æ®æ˜¯å¦å‡†å¤‡å°±ç»ªçš„æ ‡å¿—ã€‚

- `micAnalog`ï¼šåˆ›å»ºä¸€ä¸ª MicrophoneAnalog å¯¹è±¡æ¥æ§åˆ¶éº¦å…‹é£ã€‚

***å›è°ƒå‡½æ•°å£°æ˜***

```cpp
void mic_samples_ready_cb();
void calculate_and_display_voice_level();
```

- `mic_samples_ready_cb()`: é‡‡æ ·å®Œæˆæ—¶è°ƒç”¨çš„å›è°ƒå‡½æ•°ã€‚

- `calculate_and_display_voice_level()`: è®¡ç®—éŸ³é‡å¹¶æ§åˆ¶LEDäº®åº¦çš„å‡½æ•°ã€‚

***setup() å‡½æ•°***

```cpp
void setup()
{
  Serial.begin(115200);
  pinMode(LED_BUILTIN, OUTPUT);

  micAnalog.begin(mic_buffer, NUM_SAMPLES);
  Serial.println("Microphone initialized...");

  micAnalog.startSampling(mic_samples_ready_cb);
  Serial.println("Sampling started...");
}
```

- ä»¥115200çš„æ³¢ç‰¹ç‡åˆå§‹åŒ–ä¸²è¡Œé€šä¿¡ã€‚

- å°†æ¿è½½LEDå¼•è„šè®¾ç½®ä¸ºè¾“å‡ºæ¨¡å¼ã€‚

- åˆå§‹åŒ–éº¦å…‹é£å¹¶æŒ‡å®šé‡‡æ ·ç¼“å†²åŒºã€‚

- å¼€å§‹é‡‡æ ·å¹¶è®¾ç½®é‡‡æ ·å®Œæˆæ—¶çš„å›è°ƒå‡½æ•°ã€‚

***loop()å‡½æ•°***

```cpp
void loop()
{
  if (data_ready_flag) {
    data_ready_flag = false;
    calculate_and_display_voice_level();
  }
}
```

- æ£€æŸ¥ `data_ready_flag` æ˜¯å¦ä¸º `true`ï¼Œè¡¨ç¤ºæ–°æ•°æ®å·²å‡†å¤‡å°±ç»ªã€‚

- å¦‚æœæœ‰æ–°æ•°æ®å¯ç”¨ï¼Œè°ƒç”¨ `calculate_and_display_voice_level()` å‡½æ•°æ¥å¤„ç†æ•°æ®ã€‚

```cpp

void mic_samples_ready_cb()
{
  memcpy(mic_buffer_local, mic_buffer, NUM_SAMPLES * sizeof(uint32_t));
  data_ready_flag = true;
}
```

å°†æ ·æœ¬æ•°æ®ä» `mic_buffer` å¤åˆ¶åˆ° `mic_buffer_local` ä»¥é˜²æ­¢è¦†ç›–ã€‚

å°† `data_ready_flag` è®¾ç½®ä¸º `true` ä»¥æŒ‡ç¤ºæ–°æ•°æ®å·²å‡†å¤‡å°±ç»ªã€‚

```cpp

void calculate_and_display_voice_level() {
  static uint32_t rolling_average = 0u;

  micAnalog.stopSampling();

  uint32_t voice_level = (uint32_t)micAnalog.getAverage(mic_buffer_local, NUM_SAMPLES);
  voice_level = constrain(voice_level, MIC_VALUE_MIN, MIC_VALUE_MAX);
  rolling_average = (voice_level + rolling_average) / 2;

  int brightness = map(rolling_average, MIC_VALUE_MIN, MIC_VALUE_MAX, 0, 255);
  if (LED_BUILTIN_ACTIVE == LOW) {
    analogWrite(LED_BUILTIN, 255 - brightness);
  } else {
    analogWrite(LED_BUILTIN, brightness);
  }
  Serial.println(rolling_average);

  micAnalog.startSampling(mic_samples_ready_cb);
}
```

- åœæ­¢é‡‡æ ·ä»¥é˜²æ­¢æ•°æ®è¦†ç›–ã€‚

- è®¡ç®—é‡‡æ ·æ•°æ®çš„å¹³å‡å€¼å¹¶å°†å…¶çº¦æŸåœ¨ `MIC_VALUE_MIN` å’Œ `MIC_VALUE_MAX` ä¹‹é—´ã€‚

- è®¡ç®—æ»šåŠ¨å¹³å‡å€¼ä»¥å¹³æ»‘éŸ³é‡å˜åŒ–ã€‚

- å°†æ»šåŠ¨å¹³å‡å€¼æ˜ å°„åˆ°LEDäº®åº¦èŒƒå›´ï¼ˆ0åˆ°255ï¼‰å¹¶è°ƒæ•´LEDäº®åº¦ã€‚

- é€šè¿‡ä¸²å£è¾“å‡ºæ»šåŠ¨å¹³å‡å€¼ä»¥è§‚å¯ŸéŸ³é‡å˜åŒ–ã€‚

- é‡æ–°å¼€å§‹é‡‡æ ·ä»¥æ”¶é›†æ–°çš„éŸ³é¢‘æ•°æ®ã€‚

### ç»“æœå›¾è¡¨

å½“æˆ‘ä»¬å¯¹ç€éº¦å…‹é£å¹æ°”æ—¶ï¼Œå¯ä»¥çœ‹åˆ°é¡¶éƒ¨çš„LEDä¼šéšç€å£°éŸ³å˜äº®å’Œå˜æš—ã€‚

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/mic.gif" style={{width:500, height:'auto'}}/></div>

### æ›´å¤šå†…å®¹

å¦‚æœæ‚¨æƒ³è¦æ›´å¤šç¤ºä¾‹ä»£ç ï¼Œè¯·ç‚¹å‡»ï¼š-> **"Example -> SilabsMicrophoneAnalog -> MicrophoneVolume"**

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/mic_arduino.jpg" style={{width:500, height:'auto'}}/></div>

## èµ„æº

### é€‚ç”¨äº Seeed Studio XIAO MG24 Sense

- ğŸ“„ **[PDF]** [Seeed Studio 6è½´IMU(LSM6DS3TR-C) æ•°æ®æ‰‹å†Œ](https://statics3.seeedstudio.com/fusion/opl/sheets/314040211.pdf)
- ğŸ“„ **[PDF]** [Seeed Studio æ¨¡æ‹Ÿéº¦å…‹é£(MSM381ACT001) æ•°æ®æ‰‹å†Œ](https://files.seeedstudio.com/wiki/mg24_mic/312030602_MEMSensing_MSM381ACT001_Datasheet.pdf)

## æŠ€æœ¯æ”¯æŒä¸äº§å“è®¨è®º

æ„Ÿè°¢æ‚¨é€‰æ‹©æˆ‘ä»¬çš„äº§å“ï¼æˆ‘ä»¬åœ¨è¿™é‡Œä¸ºæ‚¨æä¾›ä¸åŒçš„æ”¯æŒï¼Œä»¥ç¡®ä¿æ‚¨ä½¿ç”¨æˆ‘ä»¬äº§å“çš„ä½“éªŒå°½å¯èƒ½é¡ºç•…ã€‚æˆ‘ä»¬æä¾›å¤šç§æ²Ÿé€šæ¸ é“ï¼Œä»¥æ»¡è¶³ä¸åŒçš„åå¥½å’Œéœ€æ±‚ã€‚

<div class="button_tech_support_container">
<a href="https://forum.seeedstudio.com/" class="button_forum"></a>
<a href="https://www.seeedstudio.com/contacts" class="button_email"></a>
</div>

<div class="button_tech_support_container">
<a href="https://discord.gg/eWkprNDMU7" class="button_discord"></a>
<a href="https://github.com/Seeed-Studio/wiki-documents/discussions/69" class="button_discussion"></a>
</div>
